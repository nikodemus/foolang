import .object.Object

interface Function
    is Object

    -- Note: implementing #value methods directly is very
    -- likely worth it!

    required method apply: arguments
    required method arity

    -- Delegation to #apply

    method optional: arg1
        self arity is 1
            ifTrue: { self apply: [arg1] }
            ifFalse: { self apply: [] }!

    method value: arg1 optional: arg2
        self arity is 1
            ifTrue: { self apply: [arg1] }
            ifFalse: { self apply: [arg1, arg2] }!

    method optional: arg1 value: arg2
        self arity is 1
            ifTrue: { self apply: [arg2] }
            ifFalse: { self apply: [arg1, arg2] }!

    method value
        self apply: []!

    method value: arg1
        self apply: [arg1]!

    method value: arg1 value: arg2
        self apply: [arg1, arg2]!

    method value: arg1 value: arg2 value: arg3
        self apply: [arg1, arg2, arg3]!
end

interface Block
    is Function

    required method loop
    required method finally: cleanup

    -- Delegation to #loop

    method whileTrue: body
        let r = False.
        { self value is True
              ifFalse: { return r }.
          r = body value }
            loop!

    method whileFalse: body
        let r = False.
        { self value is False
              ifFalse: { return r }.
          r = body value }
            loop!

    method whileTrue
        { self value is True
              ifFalse: { return False } }
            loop!

    method whileFalse
        { self value is False
              ifFalse: { return False } }
            loop!

    -- Miscellany

    method after: block
        let res = self value.
        block value.
        res!

    method with: value
        -- Deserves a better verb than close, should probably
        -- steal python's __enter__ and __exit__ style names.
        -- and protocol.
        { self value: value }
            finally: { value close }!

    method ascending
        { |a b|
          (self value: a) < (self value: b) }!

    method descending
        { |a b|
          (self value: a) > (self value: b) }!

    method on: condition do: block
        condition
            withHandler: block
            do: self!

    method on: cond1 do: block1 on: cond2 do: block2
        cond2
            withHandler: block2
            do: { cond1
                      withHandler: block1
                      do: self }!

    method catch: condition
        Catch
            block: self
            condition: condition!

    method then: then
        Composition
            block: self
            continuation: then!
end

class Catch { block condition }
    is Function

    method apply: arguments
        { block apply: arguments }
            on: condition
            do: { |ex| return ex }!

    method arity
        block arity!
end

class Composition { block continuation }
    is Function

    method apply: arguments
        continuation
            value: (block apply: arguments)!

    method arity
        block arity!
end
