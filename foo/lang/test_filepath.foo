class TestFilePath { assert dir none file }
    method testExists
        assert true: { dir exists }
               testing: "FilePath#exists (dir)".
        assert false: { none exists }
               testing: "FilePath#exists (none)".
        assert true: { file exists }
               testing: "FilePath#exists (file)"!

    method testIsDirectory
        assert true: { dir isDirectory }
               testing: "FilePath#isDirectory (dir)".
        assert false: { none isDirectory }
               testing: "FilePath#isDirectory (none)".
        assert false: { file isDirectory }
               testing: "FilePath#isDirectory (file)"!

    method testIsFile
        assert false: { dir isFile }
               testing: "FilePath#isFile (dir)".
        assert false: { none isFile }
               testing: "FilePath#isFile (none)".
        assert true: { file isFile }
               testing: "FilePath#isFile (file)"!

    method testTruncateExisting
        let tmpfile = dir / "Filepath-testTruncateExisting.tmp".
        tmpfile ifExists: { tmpfile deleteFile }.
        tmpfile forWrite
            create: { |f| f print: "0123456789" }.
        tmpfile forWrite
            open: { |f| f print: "00000" }.
        assert true: { tmpfile readString == "0000056789" }
               testing: "FilePath#truncateExisting (without)".
        tmpfile forWrite truncateExisting
            open: { |f| f print: "XXXXX" }.
        assert true: { tmpfile readString == "XXXXX" }!

    method testDeleteFile
        let tmpfile = dir / "FilePath-testDeleteFile.tmp".
        -- Try to clean up first.
        tmpfile ifExists: { tmpfile deleteFile }.
        assert false: { tmpfile exists }
               testing: "FilePath#deleteFile (pre1)".
        tmpfile forWrite create: { |f| f writeString: "foo" }.
        assert true: { tmpfile exists }
               testing: "FilePath#deleteFile (pre2)".
        tmpfile deleteFile.
        assert false: { tmpfile exists }
               testing: "FilePath#deleteFile (post)"!

    direct method runTests: assert in: system
        -- Assumes the repository root as the current directory
        let tests = self assert: assert
                         dir: system currentDirectory
                         none: (system currentDirectory / "does-not-exists")
                         file: (system currentDirectory / "README.md").

        tests testTruncateExisting.
        tests testDeleteFile.
        tests testExists.
        tests testIsDirectory.
        tests testIsFile!

end
