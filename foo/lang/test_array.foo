import .test_iterable.TestIterable

class TestArray { assert }
    is TestSuite

    method test_of_new_value
        let new = Array of: Float new: 3 value: 12.3.
        assert true: { new == [12.3, 12.3, 12.3] }!

    method test_new_value
        let new = Array new: 3 value: "foo".
        assert true: { new == ["foo", "foo", "foo"] }!

    method test_toString
        assert true: { [] toString == "[]" }.
        assert true: { [1,2,3] toString == "[1, 2, 3]" }!

    method test_hash
        Foolang isSelfHosted
            ifFalse: { return False }.
        assert false: { [1,2,3] hash == [1,2,2] hash }!

    method test_size
        assert true: { 0 == [] size }.
        assert true: { 3 == [1,2,3] size }!

    method test_at
        assert true: { 300 == ([100,200,300,400] at: 3) }!

    method test_at_put
        let a = [1,2,3,999,5].
        a at: 4 put: "four".
        assert true: { "four" == (a at: 4) }!

    method equalityCases
        [[ [1,2,3], [1,2,3], True ],
         [ [1, 2, [3, 4]], [1, 2, [3, 4]], True ],
         [ [1, 2, 4], [1, 2, 3], False ],
         [ [1, 2, 3, 4], [1, 2, 3], False ],
         [ ["one", 2, 3], [1, "two", 3], False ]]!

    method testEquality
        assert forAll: self equalityCases
               that: { |case|
                       (case first == case second) is case third }
               testing: "Array#=="!

    method sortingCases
        [[ [], [] ],
         [ [1], [1] ],
         [ [1, 1], [1, 1] ],
         [ [1, 2], [1, 2] ],
         [ [2, 1], [1, 2] ],
         [ [1, 2, 3], [1, 2, 3] ],
         [ [3, 2, 1], [1, 2, 3] ],
         [ [2, 1, 3], [1, 2, 3] ],
         [ [1, 3, 2], [1, 2, 3] ],
         [ [9, 2, 1, 8, 7, 3, 5, 4, 6, 0],
           [0, 1, 2, 3, 4, 5, 6, 7, 8, 9] ]]!

    method testSort
        assert forAll: self sortingCases
               that: { |case|
                       case first sort == case second }
               testing: "Array#sort return value".
        assert forAll: self sortingCases
               that: { |case|
                       case first sort.
                       case first == case second }
               testing: "Array#sort side effect"!

    method reverseCases
        [[ [], [] ],
         [ [1], [1] ],
         [ [1, 2], [2, 1] ],
         [ [1, 2, 3], [3, 2, 1] ]]!

    method testReverse
        assert forAll: self reverseCases
               that: { |case|
                       case first reverse == case second }
               testing: "Array#reverse return value".
        assert forAll: self reverseCases
               that: { |case|
                       case first reverse.
                       case first == case second }
               testing: "Array#reverse side effect"!

    method testReversed
        assert forAll: self reverseCases
               that: { |case|
                       case first reversed == case second }
               testing: "Array#reversed return value".
        assert forAll: self reverseCases
               that: { |case|
                       let orig = case first copy.
                       case first reversed.
                       case first == orig }
               testing: "Array#reversed no side effect"!
end

class TestArrayAsIterable { assert }
    is TestIterable

    method make: elements
        Array from: elements!
end
