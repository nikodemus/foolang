interface Collection

    method == other
        let size = self size.
        size is other size
            ifFalse: { return False }.
        1 to: size
          do: { |i|
                (self at: i) == (other at: i)
                    ifFalse: { return False } }.
        True

    method checkSize: collection
        let size = self size.
        size == collection size
            ifTrue: { size }
            ifFalse: { raise "Collection size mismatch" }

    method first
       self at: 1

    method second
       self at: 2

    method last
        self at: self size

    method ifEmpty: block
        self size is 0
            ifTrue: block

    method ifEmpty: block ifNotEmpty: notBlock
        self size is 0
            ifTrue: block
            ifFalse: notBlock

    method find: block
        self find: block ifNone: { False }

    method find: block ifNone: noneBlock
        self do: { |elt|
                   (block value: elt) is True
                       ifTrue: { return elt } }.
        noneBlock value

    method do: block interleaving: interBlock
        let first = True.
        self do: { |elt|
                   first
                       ifTrue: { first = False }
                       ifFalse: { interBlock value }.
                   block value: elt }

    method do: block
        1 to: self size
          do: { |i| block value: (self at: i) }.
        self

    method with: sequence do: block
        let size = self checkSize: sequence.
        1 to: size
          do: { |i|
                block value: (self at: i) value: (sequence at: i) }.
        self

    method inject: value into: block
        let result = value.
        self do: { |elt| result = (block value: result value: elt) }.
        result

    method with: sequence inject: value into: block
        let result = value.
        self with: sequence
             do: { |a b|
                   result = (block value: result value: a value: b) }.
        result

    method sum
        self inject: 0 into: { |res elt| res + elt }

    method sum: block
        self inject: 0 into: { |res elt| res + (block value: elt) }

    method with: array sum: block
        self
            with: array
            inject: 0
            into: { |res a b|
                    res + (block value: a value: b) }
end
