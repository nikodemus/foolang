import .object.Object
import .exception.TypeError
import .exception.Error
import .layout_target_ext
import .stringOutput.StringOutput
import .any.Any
import .list.List

class _ConstructorName {}
    direct method forSlots: slots
        slots
            ifEmpty: { return #new }.
        (StringOutput
             with: { |out|
                     slots
                         do: { |each|
                               out print: each name.
                               out print: ":" } })
        toSelector!
end

extend Class
    is Object

    method displayOn: stream
        stream writeString: self name!

    method typecheck: value
        -- Output debug println: "{self} typecheck: {value}".
        (self includes: value)
            ifTrue: { value }
            ifFalse: { TypeError
                           raise: value
                           expected: self }!

    method _layoutForSlots: slots
        -- Sanity check
        slots doWithIndex: { |each index|
                             each index is index
                                 assert: "Slot index must match slot position" }.
        -- Option 1: empty layout
        slots ifEmpty: { return Layout empty }.
        -- Option 2: new layout, set it to slots
        (slots noneSatisfy: { |each| each hasLayout })
            ifTrue: { let layout = Layout new: (slots size).
                      slots do: { |each| each layout: layout }.
                      return layout }.
        -- Option 3: existing layout (all slots have the same layout, this is
        -- a subclass.) FIXME: Why do I have subclasses, bah...
        let firstLayout = slots first layout.
        (slots allSatisfy: { |each| each layout is firstLayout })
            ifTrue: { return firstLayout }.
        -- Option 4: mixed layout, not acceptable.
        Error raise: "Slots have mixed layout: {slots}"!

    method new: name
           slots: slots
           interfaces: interfaces
           directMethods: directMethods
           instanceMethods: instanceMethods
        let interfaces = (interfaces includes: Any)
                             ifTrue: { interfaces }
                             ifFalse: { interfaces append: [Any] }.
        let layout = self _layoutForSlots: slots.
        let ctor = layout constructorMethod: (_ConstructorName forSlots: slots).
        let metaclass = Class
                            subclass: "{name} classOf"
                            interfaces: (interfaces collect: #classOf)
                            methods: (directMethods append: [ctor]).
        let readers = (slots reject: #isPrivate)
                          collect: { |each|
                                     layout readerMethod: each name toSelector
                                            for: each index }.
        metaclass
            new: name
            layout: layout
            interfaces: interfaces
            methods: (instanceMethods append: readers)!
end
