import .test_iterable.TestIterable

class TestByteArray { assert }
    is TestSuite

    direct method runTests: assert
        (self assert: assert) runTests!

    method test_size
        assert true: { 3 == (ByteArray new: 3) size }!

    method test_accessors
        let bytes = ByteArray new: 3.
        bytes put: -1 at: 1. -- Should be a TypeError, maybe?
        bytes put: 100 at: 2.
        bytes put: 200 at: 3.
        assert true: { 255 == (bytes at: 1) }.
        assert true: { 100 == (bytes at: 2) }.
        assert true: { 200 == (bytes at: 3) }!

    method testBasic
        assert forAll: (1 to: 10)
               that: { |n|
                       let bytes = ByteArray new: n.
                       let p = n / 2 + 1.
                       let old = bytes at: p.
                       bytes put: n at: p.
                       let new = bytes at: p.
                       (bytes size == n)
                           and: (old == 0)
                           and: (new == n)
                           and: (n > 0) }
               testing: "ByteArray creation and access"!
end

class TestByteArrayAsIterable { assert }
    is TestIterable

    method make: elements
        ByteArray from: elements!
end
