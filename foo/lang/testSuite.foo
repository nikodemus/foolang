import .list.List
import .exception.*
import .output_ext

interface TestSuite
    is Object

    direct method runTests: assert in: system
        (self assert: assert) runTestsIn: system with: []!


    direct method runTests: assert in: system with: testNames
        (self assert: assert) runTestsIn: system with: testNames!


    method system: system
        False!


    method match: full in: parts
        parts anySatisfy: { |part|
                            ((full search: part) is False) not }!


    method runTestsIn: system with: testNames
        let testSelector = testNames isEmpty
                               ifTrue: { { |_aTest| True } }
                               ifFalse: { { |aTest| self match: aTest name in: testNames } }.
        -- Extension spot for grabbing the system.
        self system: system.
        self doTests: { |each|
                        (testSelector value: each)
                            => { self runTest: each in: system } }.
        self!


    method runTest: test in: system
        let start = system clock time.
        system output print: "- ".
        system output print: test name.
        system output flush.
        test sendTo: self with: [].
        let stop = system clock time.
        system output println: " ({(stop-start) real decimalString: 3}s)"!

    method doTests: block
        let list = List new.
        Class selectorsOf: self
              do: { |each|
                    (each startsWith: "test")
                        ifTrue: { list add: each } }.
        list isEmpty
            ifTrue: { Error raise: "Not tests for {self}!" }.
        list sort do: block.
        self!
end
