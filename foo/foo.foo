import .impl.repl.REPL
import .impl.interpreter.Interpreter
import .impl.compiler.Compiler

define Options
    { "-h"        -> #help:in:,
      "--help"    -> #help:in:,
      "-c"        -> #compile:in:,
      "--compile" -> #compile:in: }!

class Main {}
    direct method run: command in: system
        command isEmpty
            => { return REPL runIn: system }.
        let selector
            = Options at: command first
                      ifNone: { #interpret:in: }.
        selector sendTo: self with: [command, system]!

    direct method interpret: command in: system
        Interpreter run: command first
                    in: system
                    with: command rest!

    direct method compile: command in: system
        let compileCommand = command rest.
        compileCommand size > 1
            ifTrue: { self abort: "Too many arguments to --compile!" in: system }.
        Compiler compile: compileCommand first
                 in: system!

    direct method abort: message in: system
        system output println: message.
        self help: [] in: system.
        system exit: 1!

    direct method help: _command in: system
        system output
            ; println: "usage: foo [option] [filename] [argument*]"
            ; newline
            ; println: "    When run without any arguments, start a REPL."
            ; newline
            ; println: "    When run with non-option arguments takes the first"
            ; println: "    one as the path to a program to run, and the"
            ; println: "    remaining ones as command-line arguments to that"
            ; newline
            ; println: "  Options:"
            ; newline
            ; println: "    -h, --help     Prints this message."
            ; println: "    -c, --compile  Compile the specified file."!
end
