import .transpiler.array
import .transpiler.block
import .transpiler.boolean
import .transpiler.float
import .transpiler.integer
import .transpiler.name.Name
import .transpiler.output
import .transpiler.string
import .ast.*
import .utils.*

define CEscapes
    { "'" -> "\\'",
      "\n" -> "\\n",
      "\r" -> "\\r" }!

-- define Tracer DebugTracer name: "CTranspiler"!
define Tracer NullTracer!

class BuiltinMethod { home selector definition isDirect }
    direct method forEach: builtins in: home direct: areDirect
        let dict = Dictionary new.
        builtins
            do: { |selector definition|
                  dict at: selector name
                       put: (self
                                 home: home
                                 selector: selector
                                 definition: definition
                                 isDirect: areDirect) }.
        dict!
    method isBuiltin
        True!
    method arity
        definition signature size!
    method frameSize
        self arity + self definition vars!
    method signature
        definition signature!
    method body
        definition body!
    method printOn: stream
        stream print: "#<BuiltinMethod {selector}>"!
    method toString
        StringOutput with: { |out| self printOn: out }!
end

class BuiltinClass { host markFunction _interfaces _directMethods _instanceMethods }
    is AstMethodHome

    direct method new: host
                  mark: mark
                  directMethods: directMethods
                  instanceMethods: instanceMethods
        let theClass = self host: host
                            markFunction: mark
                            _interfaces: List new
                            _directMethods: False
                            _instanceMethods: False.
        theClass _directMethods: directMethods.
        theClass _instanceMethods: instanceMethods.
        theClass!

    method _interfaces
        _interfaces!

    method _directMethods
        _directMethods!

    method _instanceMethods
        _instanceMethods!

    method _readerMethods
        []!

    method _directMethods: methods
        _directMethods = BuiltinMethod forEach: methods in: self direct: True!

    method _instanceMethods: methods
        _instanceMethods = BuiltinMethod forEach: methods in: self direct: False!

    method visitBy: visitor
        visitor visitBuiltin: self!

    method name
        host name!

    method eval
        host!

    method isBuiltin
        True!

    method isDynamic
        False!
end

define BuiltinClasses [
     BuiltinClass
         new: Array
         mark: "foo_mark_array"
         directMethods: array.DirectMethods
         instanceMethods: array.InstanceMethods,
     BuiltinClass
         new: Block
         mark: "foo_mark_block"
         directMethods: Dictionary new
         instanceMethods: block.InstanceMethods,
     BuiltinClass
         new: Boolean
         mark: "foo_mark_noop"
         directMethods: boolean.DirectMethods
         instanceMethods: boolean.InstanceMethods,
     BuiltinClass
         new: Float
         mark: "foo_mark_noop"
         directMethods: Dictionary new
         instanceMethods: float.FloatMethods,
     BuiltinClass
         new: Integer
         mark: "foo_mark_noop"
         directMethods: Dictionary new
         instanceMethods: integer.IntegerMethods,
     BuiltinClass
         new: Output
         mark: "foo_mark_noop"
         directMethods: output.DirectMethods
         instanceMethods: Dictionary new,
     BuiltinClass
         new: String
         mark: "foo_mark_noop"
         directMethods: string.DirectMethods
         instanceMethods: string.InstanceMethods
]!

class SelectorMap { names }
    direct method new
        self names: Dictionary new!

    method map: selector
        names
            at: selector name
            ifNonePut: { Name mangleSelector: selector }!

    method do: block
        names do: block!
end

define $home
    False!

class CTranspiler { output selectorMap blockFunctions constants variables tmpCounter
                    globals generatedMethods }


    direct method transpile: string in: env
        env removeBuiltins: [
            "Any",
            "Object",
            "StringOutput"
        ].
        env replaceBuiltins: BuiltinClasses.
        env load: string.
        self transpileMain: (env global: "Main") in: env!

    direct method transpileMain: main in: env
        let visitor = CTranspiler
                          output: StringOutput new
                          selectorMap: SelectorMap new
                          blockFunctions: List new
                          constants: List new
                          variables: Dictionary new
                          tmpCounter: 0
                          globals: Dictionary new
                          generatedMethods: Dictionary new.
        -- KLUDGE: add #writeString: to the selectors.
        visitor selectorMap map: #writeString:.
        env replaceBuiltins: BuiltinClasses.
        visitor declare: main.
        visitor writeDefinitions.
        visitor writeMain.
        { main: visitor output,
          builtins: visitor generateBuiltins,
          blocks: visitor generateBlocks,
          constants: visitor generateConstants,
          declarations: visitor generateDeclarations,
          selectors: visitor generateSelectors }!

    method addTemp
        $home addTemp!

    method declare: global
        -- Debug println: "#declare: {global}".
        globals at: (global name)
                ifNonePut: { global definition }!

    method declareDynamic: var
        self declare: var.
        variables at: var name ifNonePut: { variables size }!

    method genTemp: name
        tmpCounter = tmpCounter + 1.
        "tmp{tmpCounter}_{name}"!

    method forwardDeclarationFor: aGlobal
        -- Debug println: "declare: {aGlobal}".
        (BuiltinClass includes: aGlobal)
            ifTrue: { self _classDeclarations: aGlobal }.
        (AstClass includes: aGlobal)
            ifTrue: { self _classDeclarations: aGlobal }.
        (AstInterface includes: aGlobal)
            ifTrue: { self _interfaceDeclarations: aGlobal }.
        (AstBuiltin includes: aGlobal)
            ifTrue: { return False }.
        aGlobal isDynamic
            ifTrue: { self declareDynamic: aGlobal.
                      output println: "struct Foo {Name mangleDynamic: aGlobal};" }
            ifFalse: {  output println: "struct Foo {Name mangleGlobal: aGlobal};" }!

    method _classDeclarations: aClass
        output println: "struct FooVtable {Name mangleClassVtable: aClass};".
        output println: "struct FooVtable {Name mangleInstanceVtable: aClass};".
        let isOwnDefinition
                = { |def| def home is aClass }.
        (aClass directMethods select: isOwnDefinition)
            do: { |each| self _methodDeclaration: each }.
        (aClass instanceMethods select: isOwnDefinition)
            do: { |each| self _methodDeclaration: each }!

    method _interfaceDeclarations: anInterface
        output println: "struct FooVtable {Name mangleInterfaceVtable: anInterface};".
        output println: "struct FooVtable {Name mangleInstanceVtable: anInterface};".
        let isOwnDefinition
                = { |def| def home is anInterface and: def isRequired not }.
        (anInterface directMethods select: isOwnDefinition)
            do: { |each| self _methodDeclaration: each }.
        (anInterface instanceMethods select: isOwnDefinition)
            do: { |each| self _methodDeclaration: each }!

    method _methodDeclaration: aMethod
        output print: "struct Foo ".
        output print: (Name mangleMethod: aMethod).
        output println: "(struct FooContext*);"!

    method writeDefinitions
        -- Processing a definition can produce new global references. Visit
        -- each definition only once, finish with the full collection.
        let allGlobals = Dictionary new.
        { globals isEmpty }
            whileFalse: { let old = globals.
                          globals = Dictionary new.
                          old do: { |name def|
                                    allGlobals at: name
                                               ifNone: { allGlobals put: def at: name.
                                                         def visitBy: self } } }.
        globals = allGlobals!

    method writeMain
        output println: "int main".
        output println: "    (int argc, char** argv)".
        output println: "\{".
        output println: "    fooinit();".
        output println: "    struct FooArray* vars = FooArray_alloc({variables size});".
        variables
            do: { |name offset|
                  let var = {name: name}.
                  output print: "vars->data[{offset}] = ".
                  output print: (Name mangleDynamic: var).
                  output println: ";" }.
        output println: "    struct FooContext* ctx = foo_context_new_main(vars);".
        let main = Name mangleGlobal: {name: "Main"}.
        output println: "    foo_send(ctx, &{Name mangleSelector: #run}, {main}, 0);".
        output println: "    return 0;".
        output println: "}"!

    method generateSelectors
        let output = StringOutput new.
        let prev = "NULL".
        selectorMap do: { |name cname|
                         output print: "struct FooSelector ".
                         output print: cname.
                         output print: " = \{ .name = &FOO_CSTRING(".
                         output display: name.
                         output print: "), .next = ".
                         output print: prev.
                         output print: " };\n".
                         prev = "&{cname}" }.
        output println: "struct FooSelector* FOO_InternedSelectors = {prev};".
        output!

    method generateDeclarations
        -- Debug println: "      #generateDeclarations".
        let output = StringOutput new.
        let builtinVisitor = CTranspiler
                                 output: output
                                 selectorMap: selectorMap
                                 blockFunctions: blockFunctions
                                 constants: constants
                                 variables: variables
                                 tmpCounter: tmpCounter
                                 globals: globals
                                 generatedMethods: generatedMethods.
        -- FIXME: visit only referenced builtins
        BuiltinClasses
            do: { |each|
                  builtinVisitor forwardDeclarationFor: each }.
        globals
            doValues: { |each|
                        builtinVisitor forwardDeclarationFor: each }.
        blockFunctions
            do: { |block|
                  output print: block declaration.
                  output println: ";" }.
        output!

    method generateBuiltins
        -- Debug println: "      #generateBuiltins:".
        let output = StringOutput new.
        let builtinVisitor = CTranspiler
                                 output: output
                                 selectorMap: selectorMap
                                 blockFunctions: blockFunctions
                                 constants: constants
                                 variables: variables
                                 tmpCounter: tmpCounter
                                 globals: globals
                                 generatedMethods: generatedMethods.
        -- FIXME: used builtins only
        BuiltinClasses
            do: { |each|
                  builtinVisitor visitClassDefinition: each }.
        output!

    method generateBlocks
        self generateBlocksOn: StringOutput new!

    method generateBlocksOn: out
        blockFunctions do: { |block|
                             out println: block definition }.
        out!

    method generateConstants
        self generateConstantsOn: StringOutput new!

    method generateConstantsOn: out
        constants do: { |each| out println: each }.
        out!

    method selectorCName: selector
        self selectorMap map: selector!

    method stringCName: string
        let index = constants size + 1.
        let cname = "foostring_id{index}".
        constants
            add: (StringOutput
                      with: { |out|
                              out println: "struct FooBytes {cname}_bytes =".
                              out println: "\{".
                              out println: "    .size = {string size},".
                              out print: "    .data = \{ ".
                              string do: { |each|
                                           out print: "'".
                                           out print: (CEscapes at: each ifNone: { each }).
                                           out print: "'," }.
                              out print: "0".
                              out println: " }".
                              out println: "};".
                              out newline.
                              out println: "struct Foo {cname} =".
                              out println: "\{".
                              out println: "    .vtable = &{Name mangleInstanceVtable: String},".
                              out println: "    .datum = \{ .ptr = &{cname}_bytes }".
                              out println: "};".
                              out newline }).
        cname!

    method blockCName: block
        let $home = block.
        let output = StringOutput new.
        -- Placeholder so that recursive entry by visitor doesn't grab the
        -- same id.
        blockFunctions add: False.
        let index = blockFunctions size.
        let cname = "fooblock_id{index}".
        let signature = StringOutput new.
        signature print: "struct Foo ".
        signature print: cname.
        signature print: "(struct FooContext* ctx)".
        output print: signature.
        output println: " \{".
        -- FIXME: Given that we need to check types here, should also put the args
        -- in frame here, same as in methods.
        block argumentVariables
            do: { |var|
                  let type = var type.
                  type is Any
                      ifFalse: { output println: "    foo_vtable_typecheck(&{Name mangleInstanceVtable: type}, ctx->frame[{var index-1}]);" } }.
        output print: "    return ".
        let blockVisitor = CTranspiler
                               output: output
                               selectorMap: selectorMap
                               blockFunctions: blockFunctions
                               constants: constants
                               variables: variables
                               tmpCounter: tmpCounter
                               globals: globals
                               generatedMethods: generatedMethods.
        blockVisitor _generateTypecheck: block returnType
                     _for: block body.
        output println: ";\n}".
        blockFunctions
            put: { declaration: signature, definition: output }
            at: index.
        cname!

    method visitBuiltin: aClass
        Tracer trace: #visitBuiltin:.
        self!

    method visitDefine: aDefine
        Tracer trace: #visitDefine:.
        aDefine isDynamic
            ifTrue: { output println: "struct Foo {Name mangleDynamic: aDefine} = " }
            ifFalse: { output println: "struct Foo {Name mangleGlobal: aDefine} = " }.
        self visitConstant: { value: aDefine eval }. -- FIXME, harmonize
        output println: ";".
        output newline!

    method visitInterfaceDefinition: anInterface
        Tracer visitInterfaceDefinition: anInterface name.
        output println: "/**
* {anInterface name} (an interface)
*
*/".
        let interfaceVtableName = Name mangleInterfaceVtable: anInterface.
        let instanceVtableName = Name mangleInstanceVtable: anInterface.
        let globalName = Name mangleGlobal: anInterface.
        let directMethods = anInterface directMethods reject: #isRequired.
        directMethods
            do: { |each| self _generateMethod: each }.
        (anInterface instanceMethods reject: #isRequired)
            do: { |each| self _generateMethod: each }.
        -- Interface Vtable
        output println: "struct FooVtable {interfaceVtableName} = ".
        output println: "\{".
        output println: "    .name = &FOO_CSTRING({anInterface name displayString}\" interface\"),".
        output println: "    .inherited = \{ .size = 0, .data = NULL },".
        output println: "    .size = {anInterface directMethods size},".
        output println: "    .mark = foo_mark_noop,".
        output println: "    .methods = \{".
        directMethods
            do: { |each|
                  output println: "        (struct FooMethod)\{ .selector = &{self selectorCName: each selector},".
                  output println: "                            .argCount = {each arity},".
                  output println: "                            .frameSize = {each frameSize},".
                  output println: "                            .function = &{Name mangleMethod: each} }," }.
        output println: "    }".
        output println: "};".
        output newline.
        -- Instance Vtable (empty, used for identity only)!
        output println: "struct FooVtable {instanceVtableName} = ".
        output println: "\{".
        output println: "    .name = &FOO_CSTRING({anInterface name displayString}),".
        output println: "    .classptr = NULL,". -- FIXME
        output println: "    .inherited = \{ .size = 0, .data = NULL },".
        output println: "    .mark = foo_mark_noop,".
        output println: "    .size = 0,".
        output println: "    .methods = \{}".
        output println: "};".
        output newline.
        -- Interface struct
        let interfaceName = "FooInterface_{anInterface name}".
        output println: "struct FooInterface {interfaceName} = \{};".
        output newline.
        -- Interface object
        output println: "struct Foo {globalName} =".
        output println: "\{".
        output println: "    .vtable = &{interfaceVtableName},".
        output println: "    .datum = \{ .ptr = &{interfaceName} }".
        output println: "};".
        output newline!

    method visitClassDefinition: aClass
        Tracer visitClassDefinition: aClass name.
        let classNote = aClass isBuiltin
                       ifTrue: { "{aClass name} (builtin)" }
                       ifFalse: { aClass name }.
        output println: "/**
 * {classNote}
 *
 */".
        let instanceVtableName = Name mangleInstanceVtable: aClass.
        let classVtableName = Name mangleClassVtable: aClass.
        let globalName = Name mangleGlobal: aClass.
        -- Augmented later with ctor and typetest
        -- Debug println: "/class direct methods".
        let directMethods = aClass directMethods asList.
        directMethods
            do: { |each| self _generateMethod: each }.
        -- Debug println: "/class instance methods".
        aClass instanceMethods
            do: { |each| self _generateMethod: each }.
        aClass isBuiltin
            ifFalse: {
                -- Layout
                let layoutName = "FooLayout_{aClass name}".
                output println: "struct FooLayout {layoutName} =".
                output println: "\{".
                output println: "    .size = {aClass slots size},".
                output println: "    .slots = \{".
                aClass slots
                    do: { |each|
                          output println: "        \{ .name = &FOO_CSTRING({each name displayString}) }," }.
                output println: "    }".
                output println: "};".
                output newline.
                -- Constructor
                let ctor = { home: aClass,
                             selector: aClass constructor,
                             arity: aClass slots size,
                             frameSize: aClass slots size,
                             isDirect: True }.
                directMethods push: ctor.
                output print: "struct Foo ".
                output println: (Name mangleMethod: ctor).
                output println: "    (struct FooContext* ctx)".
                output println: "\{".
                output println: "    (void)ctx;".
                output println: "    struct FooArray* new = FooArray_alloc({aClass slots size} * sizeof(struct Foo));".
                aClass slots
                    doWithIndex: { |each index|
                                   let type = each type.
                                   -- FIXME: visit the type
                                   type is Any
                                       ifTrue: { output println: "new->data[{index-1}] = ctx->frame[{index-1}];" }
                                       ifFalse: { output println: "new->data[{index -1}] = foo_vtable_typecheck(&{Name mangleInstanceVtable: type}, ctx->frame[{index-1}]);" } }.
                output println: "    return (struct Foo)\{ .vtable = &{instanceVtableName}, .datum = \{ .ptr = new } };".
                output println: "}".
                output newline.
                -- #includes: (typetest)
                let includes = { home: aClass,
                                 selector: #includes:,
                                 arity: 1,
                                 frameSize: 1,
                                 isDirect: True }.
                directMethods push: includes.
                output print: "struct Foo ".
                output println: (Name mangleMethod: includes).
                output println: "    (struct FooContext* ctx)".
                output println: "\{".
                output println: "    return foo_Boolean_new(&{Name mangleInstanceVtable: aClass} == ctx->frame[0].vtable);".
                output println: "}".
                output newline
            }.
        -- Interfaces
        let interfaces = aClass interfaceGlobals.
        let classInterfacesName = "FooClassInterfaces_{aClass name}".
        output println: "struct FooVtable* {classInterfacesName}[] = \{".
        interfaces
            do: { |each|
                  self declare: each.
                  output println: "    &{Name mangleInterfaceVtable: each}," }.
        output println: "};".
        output newline.
        let instanceInterfacesName = "FooInstanceInterfaces_{aClass name}".
        output println: "struct FooVtable* {instanceInterfacesName}[] = \{".
        interfaces
            do: { |each|
                  output println: "    &{Name mangleInstanceVtable: each}," }.
        output println: "};".
        output newline.
        -- Class Vtable
        output println: "struct FooVtable {classVtableName} = ".
        output println: "\{".
        output println: "    .name = &FOO_CSTRING({aClass name displayString}\" class\"),".
        output println: "    .classptr = NULL,". -- FIXME
        output println: "    .inherited = \{ .size = {interfaces size}, .data = {classInterfacesName} },".
        output println: "    .mark = foo_mark_noop,".
        output println: "    .size = {directMethods size},".
        output println: "    .methods = \{".
        directMethods
            do: { |each|
                  output println: "        (struct FooMethod)\{ .selector = &{self selectorCName: each selector},".
                  output println: "                            .argCount = {each arity},".
                  output println: "                            .frameSize = {each frameSize},".
                  output println: "                            .function = &{Name mangleMethod: each} }," }.
        output println: "    }".
        output println: "};".
        output newline.
        -- Instance Vtable
        output println: "struct FooVtable {instanceVtableName} = ".
        output println: "\{".
        output println: "    .name = &FOO_CSTRING({aClass name displayString}),".
        output println: "    .classptr = &{globalName},".
        output println: "    .inherited = \{ .size = {interfaces size}, .data = {instanceInterfacesName} },".
        output println: "    .mark = {aClass markFunction},".
        output println: "    .size = {aClass instanceMethods size},".
        output println: "    .methods = \{".
        aClass instanceMethods
            do: { |each|
                  output println: "        (struct FooMethod)\{ .selector = &{self selectorCName: each selector},".
                  output println: "                            .argCount = {each arity},".
                  output println: "                            .frameSize = {each frameSize},".
                  output println: "                            .function = &{Name mangleMethod: each} }," }.
        output println: "    }".
        output println: "};".
        output newline.
        -- Class struct
        let className = "FooClass_{aClass name}".
        output println: "struct FooClass {className} = ".
        output println: "\{".
        output println: "    .instanceVtable = &{instanceVtableName}".
        output println: "};".
        output newline.
        -- Class object
        output println: "struct Foo {globalName} =".
        output println: "\{".
        output println: "    .vtable = &{classVtableName},".
        output println: "    .datum = \{ .ptr = &{className} }".
        output println: "};".
        output newline!

    method _generateMethod: aMethod
        Tracer _generateMethod: aMethod.
        let name = Name mangleMethod: aMethod.
        -- Debug println: "_generateMethod: {name}".
        generatedMethods
            at: name
            ifNonePut: { self _actuallyGenerateMethod: aMethod as: name }!

    method _actuallyGenerateMethod: aMethod as: name
        -- Debug println: "_actuallyGenerateMethod: {aMethod}".
        let $home = aMethod.
        output print: "struct Foo ".
        output println: name.
        output println: "    (struct FooContext* ctx)".
        output println: "\{".
        output println: "    (void)ctx;".
        aMethod signature
            doWithIndex: { |type index|
                           -- FIXME: visit the type
                           type is Any
                               ifFalse: { output println: "    foo_vtable_typecheck(&{Name mangleInstanceVtable: type}, ctx->frame[{index-1}ul]);" } }.
        aMethod isBuiltin
            ifTrue: { -- Debug println: "  - builtin method".
                      output print: "    ".
                      output println: (aMethod body replace: "\n" with: "\n    ").
                      output println: "}" }
            ifFalse: { -- Debug println: "  - normal method".
                       output print: "    return ".
                       self _generateTypecheck: aMethod returnType
                            _for: aMethod body.
                       output println: ";".
                       output println: "}" }.
        -- Debug println: "  => method done".
        output newline.
        aMethod!

    method _generateTypecheck: type _for: value
        -- Debug println: "_generateTypecheck: {type} _for: {value}".
        type is Any
            ifTrue: { return value visitBy: self }.
        let type = type::AstGlobal.
        self declare: type.
        output print: "foo_vtable_typecheck(&".
        output print: (Name mangleInstanceVtable: type).
        output print: ", ".
        value visitBy: self.
        output print: ")"!

    method visitGlobal: aVar
        Tracer trace: #visitGlobal:.
        self declare: aVar.
        output print: (Name mangleGlobal: aVar)!

    method visitDynamic: aVar
        Tracer trace: #visitDynamic:.
        let index = self declareDynamic: aVar.
        output print: "ctx->vars->data[{index}]"!

    method visitTypecheck: aCheck
        Tracer visitTypecheck: aCheck.
        self _generateTypecheck: aCheck type
             _for: aCheck value!

    method visitIs: anIs
        Tracer trace: #visitIs:.
        let left = self addTemp.
        output print: "(\{ ctx->frame[{left}] = ".
        anIs left visitBy: self.
        output print: "; ".
        let right = self addTemp.
        output print: "ctx->frame[{right}] = ".
        anIs right visitBy: self.
        output print: "; ".
        output print: "foo_Boolean_new(foo_eq(ctx->frame[{left}], ctx->frame[{right}])); ".
        output print: "})"!

    method visitSeq: seq
        Tracer trace: #visitSeq:.
        output print: "(\{ ".
        seq first visitBy: self.
        output print: "; ".
        seq then visitBy: self.
        output print: "; })"!

    method visitSlotSet: set
        Tracer trace: #visitSlotSet:.
        output print: "(PTR(FooArray, ctx->receiver.datum)->data[".
        output print: (set slot index - 1).
        output print: "] = ".
        self _generateTypecheck: set slot type
             _for: set value.
        output print: ")"!

    method visitSlotRef: ref
        Tracer trace: #visitSlotRef:.
        output print: "PTR(FooArray, ctx->receiver.datum)->data[".
        output print: (ref slot index - 1).
        output print: "]"!

    method visitReturn: ret
        Tracer trace: #visitReturn:.
        output print: "foo_return(ctx, ".
        self _generateTypecheck: ret from returnType
             _for: ret value.
        output print: ")"!

    method visitComment: aComment
        Tracer trace: #visitComment:.
        -- FIXME: Include comments in generated source.
        aComment value visitBy: self!

    method visitDatum: value
        Tracer trace: #visitDatum:.
        (Integer includes: value)
            ifTrue: { return output print: ".int64 = {value}" }.
        (Float includes: value)
            ifTrue: { return output print: ".float64 = {value}" }.
        (String includes: value)
            ifTrue: { return output print: ".ptr = &{self stringCName: value}_bytes" }.
        (Boolean includes: value)
            ifTrue: { let bit = value ifTrue: { 1 } ifFalse: { 0 }.
                      return output print: ".boolean = {bit}" }.
        Error raise: "Don't know how to transpile constant `{value}` into C."!

    method visitConstant: aConstant
        Tracer trace: #visitConstant:.
        let value = aConstant value.
        output print: "(struct Foo)\{ .vtable = &{Name mangleInstanceVtable: value classOf}, .datum = \{ ".
        self visitDatum: value.
        output println: " } }".
        output newline!

    method visitSend: aSend
        Tracer visitSend: aSend.
        output print: "(\{ ".
        let argVars = aSend arguments
                          collect: { |each|
                                     let index = self addTemp.
                                     output print: "ctx->frame[{index}] = ".
                                     each visitBy: self.
                                     output print: "; ".
                                     index }.
        output print: "foo_send(ctx, &".
        output print: (self selectorCName: aSend selector).
        output print: ", ".
        aSend receiver visitBy: self.
        output print: ", ".
        output print: aSend arguments size.
        output print: "ul".
        argVars
            do: { |index|
                  output print: ", ctx->frame[{index}]" }.
        output print: "); })"!

    method visitSelfInstance: aSelf
        Tracer visitSelfInstance: aSelf.
        output print: "ctx->receiver"!

    method visitSelfClass: aSelf
        Tracer trace: #visitSelfClass:.
        output print: "*ctx->receiver.vtable->classptr"!

    method visitBindDynamic: aBind
        Tracer trace: #visitBindDynamic:.
        let name = aBind variable name.
        let index = self declareDynamic: aBind variable.
        let unbind = self genTemp: "unbind".
        output print: "(\{ struct FooUnbind {unbind} = \{ .cleanup = \{ .function = foo_unbind, .next = ctx->cleanup }, .index = {index}, .value = ctx->vars->data[{index}] };".
        output print: " ctx->cleanup = &{unbind}.cleanup;".
        output print: " ctx->vars->data[{index}] = ".
        aBind value visitBy: self.
        output print: ";".
        let res = self addTemp.
        output print: " ctx->frame[{res}] = ".
        aBind body visitBy: self.
        output print: ";".
        output print: " assert(&{unbind}.cleanup == ctx->cleanup);".
        output print: " ctx->cleanup = ctx->cleanup->next;".
        output print: " foo_unbind(ctx, &{unbind}.cleanup);".
        output print: " ctx->frame[{res}]; })"!

    method visitBindLexical: aBind
        Tracer trace: #visitBindLexical:.
        output print: "(\{ ctx->frame[{aBind variable index - 1}ul] = ".
        self _generateTypecheck: aBind variable type
             _for: aBind value.
        output print: ";".
        aBind body visitBy: self.
        output print: "; })"!

    method visitLexicalRef: aRef
        Tracer trace: #visitLexicalRef:.
        output print: "foo_lexical_ref(ctx, {aRef variable index - 1}ul, {aRef frameOffset}ul)"!

    method visitLexicalSet: aSet
        Tracer trace: #visitLexicalSet:.
        output print: "foo_lexical_set(ctx, {aSet variable index - 1}ul, {aSet frameOffset}u, ".
        self _generateTypecheck: aSet variable type
             _for: aSet value.
        output print: ")"!

    method visitBlock: aBlock
        Tracer trace: #visitBlock:.
        output print: "foo_block_new(ctx, ".
        output print: (self blockCName: aBlock).
        output print: ", ".
        output print: aBlock argumentCount.
        output print: ", ".
        output print: aBlock frameSize.
        output print: ")"!

    method visitArray: anArray
        Tracer trace: #visitArray:.
        let entries = anArray entries.
        let array = self addTemp.
        output print: "(\{ ctx->frame[{array}] = foo_Array_alloc({entries size}); ".
        entries isEmpty
            ifFalse: { let p = self genTemp: "arrayPtr".
                       output print: "struct FooArray* {p} = PTR(FooArray, ctx->frame[{array}].datum);".
                       entries
                           doWithIndex: { |each index|
                                          output print: "{p}->data[{index-1}] = ".
                                          each visitBy: self.
                                          output print: "; " } }.
        output print: "ctx->frame[{array}]; })"!

    method visitPanic: aPanic
        Tracer trace: #visitPanic:.
        output println: "foo_panic(".
        aPanic value visitBy: self.
        output println: ")"!
end
