import .syntaxPrinter.SyntaxPrinter

interface Syntax
    is Object

    required method visitBy: visitor

    required method parts

    method checkEqualInternal: other
        self parts with: other parts
                   do: #checkEqual:!

    method isEquivalent: other
        self parts with: other parts
                   do: { |a b| a == b ifFalse: { return False } }.
        True!

    method toString
        let out = StringOutput new.
        SyntaxPrinter print: self to: out.
        out content!
end

class SyntaxLiteral { value }
    is Syntax

    method visitBy: visitor
        visitor visitLiteral: self!

    method parts
        [value]!

    method toString
        "#<SyntaxLiteral {value}>"!
end

class SyntaxHexLiteral { value }
    is Syntax

    method visitBy: visitor
        visitor visitHexLiteral: self!

    method parts
        [value]!
end

class SyntaxBinaryLiteral { value }
    is Syntax

    method visitBy: visitor
        visitor visitBinaryLiteral: self!

    method parts
        [value]!
end

class SyntaxValueTypeDeclaration { value type }
    is Syntax

    method visitBy: visitor
        visitor visitValueTypeDeclaration: self!

    method parts
        [value, type]!
end

interface SyntaxCollection
    is Syntax

    method parts
        self entries!
end

class SyntaxArray { entries }
    is SyntaxCollection

    method visitBy: visitor
        visitor visitArray: self!
end

class SyntaxRecord { entries }
    is SyntaxCollection

    method visitBy: visitor
        visitor visitRecord: self!
end

class SyntaxDictionary { entries }
    is SyntaxCollection

    method visitBy: visitor
        visitor visitDictionary: self!
end

class SyntaxSeq { first then }
    is Syntax

    method visitBy: visitor
        visitor visitSeq: self!

    method parts
        [first, then]!
end

class SyntaxReturn { value }
    is Syntax

    method visitBy: visitor
        visitor visitReturn: self!

    method parts
        [value]!
end

class SyntaxPrefixComment { comment value source }
    is Syntax

    method visitBy: visitor
        visitor visitPrefixComment: self!

    method parts
        -- Source left out intentionally, since it doesn't need to compere equal.
        [self comment, self value]!
end

class SyntaxSuffixComment { comment value source }
    is Syntax

    method visitBy: visitor
        visitor visitSuffixComment: self!

    method parts
        -- Source left out intentionally, since it doesn't need to compere equal.
        [comment, value]!
end

class SyntaxPrefix { receiver selector source }
    is Syntax

    method visitBy: visitor
        visitor visitPrefixMessage: self!

    method parts
        -- Source left out intentionally, since it doesn't need to compere equal.
        [receiver, selector]!
end

class SyntaxUnary { receiver selector source }
    is Syntax

    method visitBy: visitor
        visitor visitUnaryMessage: self!

    method parts
        -- Source left out intentionally, since it doesn't need to compere equal.
        [receiver, selector]!

    method toString
        "#<SyntaxUnary {selector} receiver: {receiver}>"!
end

class SyntaxBinary { receiver selector argument source }
    is Syntax

    method visitBy: visitor
        visitor visitBinaryMessage: self!

    method parts
        -- Source left out intentionally, since it doesn't need to compere equal.
        [receiver, selector, argument]!
end

-- FIXME: Currently multi-keyword syntax gets as source the first
-- keyword only. Should support non-contigous source regions for this.
class SyntaxKeyword { receiver selector arguments source }
    is Syntax

    method visitBy: visitor
        visitor visitKeywordMessage: self!

    method parts
        -- Source left out intentionally, since it doesn't need to compere equal.
        [receiver, selector, arguments]!
end

class SyntaxIs { left right }
    is Syntax

    method visitBy: visitor
        visitor visitIs: self!

    method parts
        [left, right]!
end

class SyntaxLet { name value body }
    is Syntax

    method visitBy: visitor
        visitor visitLet: self!

    method parts
        [value, name, body]!
end

class SyntaxSelf {}
    is Syntax

    method visitBy: visitor
        visitor visitSelf: self!

    method parts
        []!
end

class SyntaxVariable { name::String }
    is Syntax

    method visitBy: visitor
        visitor visitVariable: self!

    method parts
        [name]!
end

class SyntaxAssign { variable::String value }
    is Syntax

    method visitBy: visitor
        visitor visitAssign: self!

    method parts
        [variable, value]!
end

class SyntaxParens { body }
    is Syntax

    method visitBy: visitor
        visitor visitParens: self!

    method parts
        [body]!
end

class SyntaxBlock { parameters body }
    is Syntax

    method visitBy: visitor
        visitor visitBlock: self!

    method parts
        [parameters, body]!
end

class SyntaxDefine { name body }
    is Syntax

    method visitBy: visitor
        visitor visitDefine: self!

    method parts
        [name, body]!
end

class SyntaxClass { name slots interfaces directMethods instanceMethods }
    is Syntax

    direct method name: name slots: slots
        self
            name: name
            slots: slots
            interfaces: List new
            directMethods: List new
            instanceMethods: List new!

    method add: part
        part addTo: self!

    method visitBy: visitor
        visitor visitClass: self!

    method parts
        [name, slots, interfaces, directMethods, instanceMethods]!
end

class SyntaxInterface { name interfaces directMethods instanceMethods }
    is Syntax

    direct method name: name
        self
            name: name
            interfaces: List new
            directMethods: List new
            instanceMethods: List new!

    method add: part
        part addTo: self!

    method visitBy: visitor
        visitor visitInterface: self!

    method parts
        [name, interfaces, directMethods, instanceMethods]!
end

interface DefinitionPart
   required method addTo: definition
end

class SyntaxInterfaceRef { name }
    is Syntax
    is DefinitionPart

    method addTo: definition
        definition interfaces add: self!

    method visitBy: visitor
        visitor visitInterfaceRef: self!

    method parts
        [name]!

    method toString
        "#<SyntaxInterfaceRef {name}>"!
end

interface SyntaxMethod
    is Syntax
    is DefinitionPart

    method selector
        self signature selector!

    method visitBy: visitor
        visitor visitMethod: self!

    method parts
        [self signature, self body]!
end

class SyntaxInstanceMethod { signature body }
    is SyntaxMethod

    method addTo: thing
        thing instanceMethods add: self!

    method toString
        "#<SyntaxInstanceMethod {self selector}>"!
end

class SyntaxDirectMethod { signature body }
    is SyntaxMethod

    method addTo: thing
        thing directMethods add: self!

    method toString
        "#<SyntaxDirectMethod {self selector}>"!
end

class MethodSignature { selector parameters }
    is Object

    method isEquivalent: other
        selector == other selector and: parameters == other parameters!
end
