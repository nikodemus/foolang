---
Selector mangling for transpiler.

Underscores written as __.
Symbols prefixed with _ and translated using SymbolNames.
Keyword parts suffixed with _.
---

define SymbolNames
    { "+" -> "_add",
      "*" -> "_mul",
      "/" -> "_div",
      "-" -> "_sub",
      "%" -> "_mod",
      "=" -> "_eq",
      ">" -> "_gt",
      "<" -> "_lt",
      "^" -> "_up",
      "#" -> "_xx",
      }!

class Name { out }
    is Object

    direct method _new
        self out: StringOutput new!

    direct method mangleSelector: selector::Selector
        self _new
            ; out print: "FOO_"
            ; _mangleSelector: selector
            ; content!

    direct method mangleMethod: aMethod
        self _new
            ; _mangleMethod: aMethod
            ; content!

    direct method mangleMethodVtable: aMethod
        aMethod isDirect
            ifTrue: { self mangleDirectVtable: aMethod home }
            ifFalse: { self mangleInstanceVtable: aMethod home }!

    direct method mangleDirectVtable: aClass
        self _new
            ; _mangleDirectVtable: aClass
            ; content!

    direct method mangleInstanceVtable: aClass
        self _new
            ; _mangleInstanceVtable: aClass
            ; content!

    direct method mangleGlobal: aVar
        self _new
            ; _mangleGlobal: aVar
            ; content!

    direct method mangleDynamic: aVar
        self _new
            ; _mangleDynamic: aVar
            ; content!

    method content
        out content!

    method _mangleDynamic: aVar
        out print: "FooDynamic_".
        out print: aVar name!

    method _mangleGlobal: aVar
        out print: "FooGlobal_".
        out print: aVar name!

    method _mangleDirectVtable: aClass
        out print: "FooDirectVtable_".
        out print: aClass name!

    method _mangleInstanceVtable: aClass
        out print: "FooInstanceVtable_".
        out print: aClass name!

    method _mangleMethod: aMethod
        out print: (aMethod isDirect
                        ifTrue: { "fooDirectMethod_" }
                        ifFalse: { "fooInstanceMethod_" }).
        out print: aMethod home name.
        out print: "_".
        self _mangleSelector: aMethod selector!

    method _mangleSelector: selector::Selector
        selector isPrefix
            ifTrue: { out print: "_".
                      return self _mangleSymbol: selector }.
        selector isSymbol
            ifTrue: { return self _mangleSymbol: selector }.
        selector isKeyword
            ifTrue: { return self _mangleKeyword: selector }.
        self _mangleUnary: selector!

    method _mangleSymbol: selector
        selector name
            do: { |each|
                  out print: (SymbolNames
                                  at: each
                                  ifNone: { each }) }!

    method _mangleUnary: selector
        self _mangleName: selector name!

    method _mangleKeyword: selector
        selector parts
            do: { |part|
                  self _mangleName: (part butlast).
                  out print: "_" }!

    method _mangleName: name::String
        name do: { |s|
                   s == "_" ifTrue: { out print: s }.
                   out print: s }!
end
