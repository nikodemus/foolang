import .environment.Environment
import .parser.Parser
import .utils.Debug
import .utils.FileModuleDictionary
import .utils.StringModuleDictionary
import .syntaxTranslator.SyntaxTranslator
import .syntaxPrinter.SyntaxPrinter

extend Dictionary
    method asStringModules
        StringModuleDictionary new: self!
end

interface TestSelfHosting
    is TestSuite

    direct method runIn: system onFailure: block
        let tests = (self
                        system: system
                        ok: True
                        onFailure: block).
        tests doTests: { |each| tests runTest: each in: system }!

    direct method runIn: system
        self runIn: system onFailure: { True }!

    method checkParsingBy: block for: source
        -- Check Syntax print/parse consistency.
        -- Debug println: "/parse1".
        let syntaxList1 = block value: source.
        -- Debug println: "/pretty".
        let pretty = StringOutput
                         with: { |out|
                                 syntaxList1
                                     do: { |syntax|
                                           SyntaxPrinter print: syntax to: out }
                                     interleaving: { out newline } }.
        -- Debug println: "/parse2".
        let syntaxList2 = block value: pretty.
        -- Debug println: pretty.
        -- Debug println: "/compare".
        let res = { syntaxList1 checkEqual: syntaxList2 }
                      on: Error
                      do: { |err|
                            panic "CST print/parse inconsistency!
original:\n{source}
printed:\n{pretty}
problem: {err description}" }.
        -- Debug println: "/done".
        res!

    method parse: source using: selector expect: pretty
        -- Checks that parse pretty-prints as expected
        -- Debug println: "\n/parse source".
        let syntaxList1 = selector sendTo: Parser with: [source].
        -- Debug println: "\n/parse pretty".
        let syntaxList2 = selector sendTo: Parser with: [pretty].
        -- Debug println: "/check".
        { syntaxList1 checkEqual: syntaxList2 }
            on: Error
            do: { |err|
                  Error raise: "parse inconsistent with desired pretty-print!
source:\n{source}
pretty:\n{pretty}
problem: {err description}" }.
        let printed = StringOutput
                          with: { |out|
                                  SyntaxPrinter printAll: syntaxList1 to: out }.
        printed == pretty
            ifFalse: { let printed2 = printed replace: "\n" with: "|\n".
                       let pretty2 = pretty replace: "\n" with: "|\n".
                       printed size == pretty size
                           ifTrue: { 1 to: pretty size
                                       do: { |i|
                                             let x = printed at: i.
                                             let y = pretty at: i.
                                             x == y
                                                 ifFalse: { Error raise: "print inconsistent with desired pretty-print!
source:\n{source}
printed({x} at: {i}):\n{printed2}
pretty({y} at: {i}):\n{pretty2}" } }.
                                    panic "Never" }
                           ifFalse: { Error raise: "print inconsistent with desired pretty-print!
source:\n{source}
printed(size: {printed size}):\n{printed2}
pretty(size: {pretty size}):\n{pretty2}" } }!

    method parse: source expect: pretty
        self parse: source using: (#parseMixed:) expect: pretty!

    method parseDefinitions: source expect: pretty
        self parse: source using: (#parseDefinitions:) expect: pretty!

    method parse: source expectError: type where: test
        let result = { Parser parseExpressions: source }
                         on: type
                         do: { |e|
                               (test value: e)
                                   ifTrue: { return True }
                                   ifFalse: { Error raise: "Expected error did not pass test, got: {e}" } }.
        Error raise: "Expected error, got: {result}"!

    method eval: exprSource expect: expected
        self checkParsingBy: { |source| Parser parseExpressions: source }
             for: exprSource.
        let result = Environment new eval: exprSource.
        (result == expected)
            ifFalse: { Error raise: "Expected {expected}, got: {result}
from: '{exprSource}'" }!

    method eval: exprSource expectError: errorType where: test
        self checkParsingBy: { |source| Parser parseExpressions: source }
             for: exprSource.
        let result = { Environment new eval: exprSource }
            on: errorType
            do: { |e|
                  (test value: e)
                      ifTrue: { return True }
                      ifFalse: { Error raise: "Expected error did not pass test, got: {e}" }}.
        Error raise: "Expected error, got: {result}"!

    method modules: modules load: defSource eval: exprSource
        -- Debug println: "\n/parseDef".
        self checkParsingBy: { |source| Parser parseDefinitions: source }
             for: defSource.
        -- Debug println: "/parseExpr".
        self checkParsingBy: { |source| Parser parseExpressions: source }
             for: exprSource.
        let env = Environment modules: modules.
        -- Debug println: "/load".
        env load: defSource.
        -- Debug println: "/eval".
        env eval: exprSource!

    method load: defSource eval: exprSource
        let modules = FileModuleDictionary
                          new: { "lang" -> system files / "foo/lang" }.
        self
            modules: modules
            load: defSource
            eval: exprSource!

    method modules: modules
           load: defSource
           eval: exprSource
           expect: expected
        let result = self
                         modules: modules
                         load: defSource
                         eval: exprSource.
        (result == expected)
            ifFalse: { Error raise: "Expected {expected}, got: {result}
eval: {exprSource}
load: {defSource}
modules: {modules}" }!

    method load: defSource eval: exprSource expectError: type where: test
        { self load: defSource eval: exprSource }
            on: type
            do: { |err|
                  (test value: err)
                      ifTrue: { return True }
                      ifFalse: { Error raise: "Error did not match expectations: {err description}
eval: {exprSource}
load: {defSource}
backtrace: {err backtrace}" }}.
        Error raise: "Expected error, didn't get one
eval: {exprSource}
load: {defSource}"!

    method load: defSource eval: exprSource expect: expected
        self
            modules: Dictionary new
            load: defSource
            eval: exprSource
            expect: expected!

end
