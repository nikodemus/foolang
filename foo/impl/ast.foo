import .utils.Debug
import .astInterpreter.AstInterpreter

class StandardUndefinedPolicy {}
    -- Called during translation when encountering an undefined variable
    -- This particular method gets called when an undefined variable is
    -- encountered outside a definition.
    direct method reference: name in: environment
        Error raise: "Undefined variable: {name}"!

    -- Called during evaluation when encountering a lazily resolved undefined
    -- variable. This particilar method should never be called.
    direct method resolve: name
        panic "Unresolved undefined variable: `{name}`! This should not happen."!
end

define $UndefinedPolicy
    StandardUndefinedPolicy!

class AllowReferenceToUndefined {}
    -- During translation of definitions we allow references to undefined
    -- variables.
    direct method reference: name in: env
        let global = (AstGlobal name: name).
        env globals
            put: global
            at: name.
        return global!

    direct method resolve: name
        StandardUndefinedPolicy resolve: name!

    direct method do: block
        let $UndefinedPolicy = self.
        block value!
end

class ResolveUndefinedsWith { definitions environment }
    direct method definitions: definitions in: environment do: block
        (self definitions: definitions environment: environment)
            do: block!

    method reference: name in: environment
        StandardUndefinedPolicy reference: name in: environment!

    -- During evaluation of definition bodies we resolve undefineds to
    -- as-of-yet evaluated definitions.
    method resolve: name
        (definitions
             at: name
             ifNone: { StandardUndefinedPolicy reference: name in: environment })
        defineIn: environment!

    method do: block
        let $UndefinedPolicy = self.
        block value!
end

class UndefinedPolicy {}
    direct method reference: name in: environment
        $UndefinedPolicy reference: name in: environment!
    direct method resolve: name
        $UndefinedPolicy resolve: name!
end

interface Ast
    is Object

    method isConstant
        False!

    method debug
        Debug println: "#<AST {self}>"!
end

define $DefinitionsInProgress
    False!

class DefinitionsInProgress { this next }
    direct method with: definition do: block
        let old = $DefinitionsInProgress.
        old is False
            ifFalse: { old checkCycle: definition }.
        let $DefinitionsInProgress = DefinitionsInProgress this: definition next: old.
        block value!

    method checkCycle: definition
        this is definition
            ifTrue: { Error raise: "Cyclic definition!" }.
        next is False
            ifFalse: { next checkCycle: definition }!
end

interface AstDefinition
    is Object

    required method isDefined
    required method markDefined
    required method eval

    method do: block
        block value: self!

    method defineIn: environment
        self isDefined
            ifTrue: { (environment globals at: name) value }
            ifFalse: { DefinitionsInProgress
                           with: self
                           do: { { environment define: name value: self eval }
                                     after: { self markDefined } } } !

end

class AstDefinitionList { list }
    is AstDefinition
    direct method new: list
        self list: list!
    method isDefined
        panic "never"!
    method markDefined
        panic "never"!
    method eval
        panic "never"!
    method do: block
        list do: { |each| each do: block }!
end

class AstDefine { name body frameSize isDefined }
    is AstDefinition

    direct method name: name body: body frameSize: frameSize
        self name: name body: body frameSize: frameSize isDefined: False!

    method markDefined
        isDefined = True!

    method eval
        AstInterpreter evalDefine: body frameSize: frameSize!

    method isCanonical
        True!

    method toString
        "#<AstDefine {name}>"!
end

class AstImport { spec isDefined }
    is AstDefinition

    direct method spec: spec
        self
            spec: spec
            isDefined: False!

    method markDefined
        isDefined = True!

    method isCanonical
        False!

    method eval
        self!
end

class AstExtend { name interfaces directMethods instanceMethods isDefined }
    is AstDefinition

    direct method name: name
                  interfaces: interfaces
                  directMethods: directMethods
                  instanceMethods: instanceMethods
        self
            name: name
            interfaces: interfaces
            directMethods: directMethods
            instanceMethods: instanceMethods
            isDefined: False!

    method defineIn: environment
        self isDefined
            ifFalse: { -- FIXME: This should use a mirror for host, or
                       -- existing metaobjects for self-hosted classes.
                       DefinitionsInProgress
                           with: self
                           do: { let target = (environment globals at: name) value.
                                 interfaces do: { |each| target __addInterface: each value }.
                                 directMethods do: { |each| target __addDirectMethod: each }.
                                 instanceMethods do: { |each| target __addInstanceMethod: each }.
                                 self markDefined.
                                 target } }.
        (environment globals at: name) value!

    method markDefined
        isDefined = True!

    method isCanonical
        False!

    method eval
        panic "never, extend is different!"!

    method toString
        "#<AstExtend {name}>"!
end

class AstInterface { name interfaces directMethods instanceMethods isDefined }
    is AstDefinition

    direct method name: name
                  interfaces: interfaces
                  directMethods: directMethods
                  instanceMethods: instanceMethods
        self
            name: name
            interfaces: interfaces
            directMethods: directMethods
            instanceMethods: instanceMethods
            isDefined: False!

    method markDefined
        isDefined = True!

    method eval
        Interface
            new: name
            interfaces: (Array from: (interfaces collect: #value))
            directMethods: (Array from: directMethods)
            instanceMethods: (Array from: instanceMethods)!

    method isCanonical
        True!

    method toString
        "#<AstInterface {name}>"!
end

class AstClass { name slots interfaces directMethods instanceMethods isDefined }
    is AstDefinition

    direct method name: name
                  slots: slots
                  interfaces: interfaces
                  directMethods: directMethods
                  instanceMethods: instanceMethods
        self
            name: name
            slots: slots
            interfaces: interfaces
            directMethods: directMethods
            instanceMethods: instanceMethods
            isDefined: False!

    method markDefined
        isDefined = True!

    method eval
        -- Debug println: "slots: {(slots collect: #type) asArray displayString}".
        Class
            new: name
            slots: slots asArray
            interfaces: (Array from: (interfaces collect: #value))
            directMethods: (Array from: directMethods)
            instanceMethods: (Array from: instanceMethods)!

    method isCanonical
        True!

    method toString
        "#<AstClass {name}>"!
end

class AstMethod { name argumentVariables returnType body frameSize }
    is Object

    direct method selector: selector
                  argumentVariables: argumentVariables
                  returnType: returnType
                  body: body
                  frameSize: frameSize
        self name: selector name
             argumentVariables: argumentVariables
             returnType: returnType
             body: body
             frameSize: frameSize!

    method selector
        Selector name: name!

    method invoke: arguments on: receiver
        AstInterpreter
            invokeMethod: self
            on: receiver
            with: arguments!

    method argumentCount
        argumentVariables size!
end

interface AstNode
    is Ast

    required method visitBy: visitor
end

class AstComment { comment value source }
    is AstNode

    method visitBy: visitor
        visitor visitComment: self!
end

class AstArray { entries }
    is AstNode

    method visitBy: visitor
        visitor visitArray: self!

    method parts
        entries!
end

class AstTypecheck { value type }
    is AstNode

    method visitBy: visitor
        visitor visitTypecheck: self!

    method parts
        [value, type]!
end

class AstRecord { name entries }
    is AstNode

    method visitBy: visitor
        visitor visitRecord: self!

    method parts
        [name, entries]!
end

class AstDictionary { entries }
    is AstNode

    method visitBy: visitor
        visitor visitDictionary: self!

    method parts
        entries!
end

define AstUndefinedMarker
    -- FIXME: When constant coalescing starts happening, this needs
    -- protection.
    ["<AstUndefineMarker>"]!

class AstGlobal { name _value }
    is AstNode

    direct method name: name
        self
            name: name
            value: AstUndefinedMarker!

    direct method name: name value: value
        self
            name: name
            _value: value!

    method isUndefined
        _value is AstUndefinedMarker!

    method value
        self isUndefined
            ifTrue: { return UndefinedPolicy resolve: self name }.
        _value!

    method isConstant
        True!

    method value: newValue
        (_value is AstUndefinedMarker) assert.
        _value = newValue!

    method visitBy: visitor
        visitor visitGlobal: self!

    method referenceUpwards: _offset
        self!
end

class AstConstantRef { value }
    is AstNode

    method visitBy: visitor
        visitor visitConstant: self!

    method isConstant
        True!
end

class AstIs { left right }
    is AstNode

    method visitBy: visitor
        visitor visitIs: self!
end

class AstSeq { first then }
    is AstNode

    method visitBy: visitor
        visitor visitSeq: self!
end

class AstReturn { value }
    is AstNode

    method visitBy: visitor
        visitor visitReturn: self!
end

class AstPanic { value }
    is AstNode

    method visitBy: visitor
        visitor visitPanic: self!
end

class AstSend {
        receiver
        selector
        arguments
        source
    }
    is AstNode

    method visitBy: visitor
        visitor visitSend: self!
end

class AstBlock { body argumentVariables returnType frameSize }
    is AstNode

    method selector
        self argumentCount is 0
            ifTrue: { return #value }.
        self argumentCount is 1
            ifTrue: { return #value: }.
        self argumentCount is 2
            ifTrue: { return #value:value: }.
        self argumentCount is 3
            ifTrue: { return #value:value:value: }.
        #apply:!

    method visitBy: visitor
        visitor visitBlock: self!

    method argumentCount
        argumentVariables size!
end

class AstSelfRef {}
    is AstNode

    method visitBy: visitor
        visitor visitSelf: self!
end

class AstSlotRef { slot }
    is AstNode

    method visitBy: visitor
        visitor visitSlotRef: self!

    method assign: value
        AstSlotSet
            slot: slot
            value: value!
end

class AstSlotSet { slot value }
    is AstNode

    method visitBy: visitor
        visitor visitSlotSet: self!
end

class AstBindLexical { variable value body }
    is AstNode

    method visitBy: visitor
        visitor visitBindLexical: self!
end

class AstLexicalRef { variable frameOffset::Integer }
    is AstNode

    method visitBy: visitor
        visitor visitLexicalRef: self!

    method assign: value
        AstLexicalSet
            variable: variable
            frameOffset: frameOffset
            value: value!
end

class AstLexicalSet { variable frameOffset::Integer value }
    is AstNode

    method visitBy: visitor
        visitor visitLexicalSet: self!
end
