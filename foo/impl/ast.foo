import .utils.Debug
import .transpiler.name.Name
import .astInterpreter.AstInterpreter
import .c3.C3Linearization
import .types.*

class CompilerInfo { tag properties }
    direct method new
        self tag: False
             properties: List new!

    method contains: property
        properties contains: property!

    method add: property
        properties add: property!

    method tag: newTag
        tag = newTag.
        self!
end

interface Ast
    is Object

    method type
        TheUnknownType!

    method evalIn: env
        AstInterpreter eval: self in: env!

    method isConstant
        False!
end

interface AstToplevel
    is Object

    method evalIn: env
        self defineIn: env!

    method isBuiltin
        False!

    method isDynamic
        False!
end

-- FIXME: Would it be better to have ImportDescription and
-- compose that into both AstImport and SyntaxImport?
class AstImport { syntax }
    is AstToplevel

    method defineIn: env
        let name = self name.
        name is False
            => { return env import: self module
                            relative: self relative }.
        name == "*"
            => { return env importAll: self module
                            relative: self relative }.
        env import: name
            from: self module
            relative: self relative
            source: syntax source!

    method name
        syntax name!

    method relative
        syntax relative!

    method module
        syntax module!

    method spec
        syntax spec!

    method toString
        "#<AstImport {self spec}>"!
end

class AstExtend { type interfaces directMethods instanceMethods }
    is AstToplevel

    method defineIn: env
        -- FIXME: This should use a mirror for host, or
        -- existing metaobjects for self-hosted classes.
        let target = type definition.
        -- KLUDGE: in case of adding interfaces to AstMethodHome
        -- we want to have the definition instead of the interface
        -- object. Consider this more fallout from lack of
        -- mirrors / reflective capabilities.
        (AstMethodHome includes: target)
            ifTrue: { interfaces
                          do: { |each| target __addInterface: each } }
            ifFalse: { interfaces
                          do: { |each| target __addInterface: each eval } }.
        directMethods do: { |each| target __addDirectMethod: each }.
        instanceMethods do: { |each| target __addInstanceMethod: each }.
        target!

    method toString
        "#<AstExtend {type name}>"!
end

interface AstMethodHome
    is Object

    required method _interfaces
    required method _directMethods
    required method _instanceMethods
    required method _directMethods: methods
    required method _instanceMethods: methods
    required method _readerMethods
    required method _allInterfaces
    required method _allInterfaces: interfaces

    method type
        DefinitionType definition: self!

    method findInstanceMethod: selector
        self instanceMethods
            do: { |each|
                  each selector == selector
                      ifTrue: { return each } }.
        False!

    method findDirectMethod: selector
        self directMethods
            do: { |each|
                  each selector == selector
                      ifTrue: { return each } }.
        False!

    method _maybeAddMethod: newMethod to: aDictionary
        let name = newMethod selector name.
        let oldMethod = aDictionary
                            at: name
                            ifNone: { aDictionary
                                          put: newMethod
                                          at: name.
                                      return True }.
        oldMethod isRequired
            ifTrue: { aDictionary
                          put: newMethod
                          at: name.
                      return True }.
        False!

    method __addDirectMethod: aMethod
        self _maybeAddMethod: aMethod to: self _directMethods!

    method __addInstanceMethod: aMethod
        self _maybeAddMethod: aMethod to: self _instanceMethods!

    method __addInterface: anInterface
        -- Order is important, so these must be kept in a list!
        let interfaces = self _interfaces.
        (interfaces contains: anInterface)
            ifTrue: { return False }.
        self _interfaces push: anInterface!

    method directMethods: methods
        (self _directMethods is False) assert: "Direct methods already set.".
        self _directMethods: (self _methodDictionary: methods)!

    method instanceMethods: methods
        (self _instanceMethods is False) assert: "Instance methods already set.".
        self _instanceMethods: (self _methodDictionary: methods)!

    method allInterfaces
        self _allInterfaces is False
            ifTrue: { let list = (C3Linearization allInterfaces: self) asList.
                      (list first is self) assert.
                      self _allInterfaces: list rest }.
        self _allInterfaces!

    method ownInterfaces
        self _interfaces!

    method interfaceDefinitions
        self allInterfaces collect: #definition as: Array!

    method interfaceGlobals
        self allInterfaces collect: #yourself as: Array!

    method interfaceObjects
        self allInterfaces collect: #eval as: Array!

    method _ownDirectMethods
        let dict = Dictionary new.
        (self isBuiltin not and: self isClass)
            ifTrue: { dict put: (AstConstructorMethod home: self)
                           at: self constructor }.
        self _collectMethods: self _directMethods
             _into: dict.
        dict!

    method ownDirectMethods
        self _ownDirectMethods values!

    method directMethods
        let dict = self _ownDirectMethods.
        self _collectFromInterfaces: #directMethods
            _into: dict.
        self _collectMethods: self _standardDirectMethods
             _into: dict.
        dict values
            sort: { |each| each selector name }
                      ascending!

    method _standardDirectMethods
        StandardDirectMethodSelectors
            collect: { |each|
                       StandardDirectMethod
                           -- FIXME: Again a host/target hack
                           selector: each toSelector
                           home: self }!

    ---
    KLUDGE & FIXME: does not include reader methods, since bootstrap host
    class creation creates those automatically.
    ---
    method _ownInstanceMethods
        let dict = Dictionary new.
        self _collectMethods: self _instanceMethods
             _into: dict.
        dict!

    method ownInstanceMethods
        self _ownInstanceMethods values!

    method instanceMethods
        let dict = self _ownInstanceMethods.
        self _collectMethods: self _readerMethods
             _into: dict.
        self _collectFromInterfaces: #instanceMethods
            _into: dict.
        dict values
            sort: { |each| each selector name }
                      ascending!

    method _collectFromInterfaces: selector _into: dict
        self interfaceDefinitions :: Ordered
            do: { |anInterface|
                  self _collectMethods: (selector value: anInterface)
                       _into: dict }.
        dict!

    method _collectMethods: methods _into: dict
        methods
            do: { |aMethod|
                  -- KLUDGE Host dictionaries don't play well with
                  -- selectors.
                  dict at: aMethod selector name
                       ifNonePut: { aMethod } }!

    method _methodDictionary: methods
        Dictionary
            keys: (methods collect: { |each| each selector name })
            values: methods!
end

class AstBuiltin { global value compilerInfo }
    is AstMethodHome

    direct method global: global value: value
        self global: global
             value: value
             compilerInfo: List new!

    method eval
        global hasValue
            ifTrue: { global eval }
            ifFalse: { global value: value }!

    method isBuiltin
        True!

    method _interfaces
        value interfaces collect: { |each| { value: each } }!

    method _directMethods
        value _directMethods!

    method _instanceMethods
        value _instanceMethods!

    method _directMethods: newMethods
        panic "nope: AstBuiltin#_directMethods:"!

    method _instanceMethods: newMethods
        panic "nope: AstBuiltin#_instanceMethods:"!

    method _readerMethods
        panic "nope: AstBuiltin#_readerMethods"!

    method _allInterfaces
        panic "nope: AstBuiltin#_allInterfaces"!

    method _allInterfaces: newInterfaces
        panic "nope: AstBuiltin#_allInterfaces:"!

    method definitionIn: env
        (env globals at: (value name)) definition!

    method visitBy: visitor
        visitor visitBuiltin: self!

    method __addInterface: anInterface
        value definition __addInterface: anInterface!

    method __addDirectMethod: aDirectMethod
        value definition __addDirectMethod: aDirectMethod!

    method __addInstanceMethod: anInstanceMethod
        value definition __addInstanceMethod: anInstanceMethod!
end

interface AstDefinition
    is AstToplevel

    method global: newGlobal
        self global is newGlobal
            assert: "{Self name}#global cannot be changed."!

    method idOr: block
        self global idOr: block!

    method defineIn: env
        self global isDefined assert: "undefined global: {self}"!

    method evalIn: env
        self defineIn: env.
        self eval!

    method definition
        self!
end

class AstBuiltin { global value compilerInfo }
    is AstDefinition

    direct method global: global value: value
        self global: global
             value: value
             compilerInfo: CompilerInfo new!

    method eval
        global hasValue
            ifTrue: { global eval }
            ifFalse: { global value: value }!

    method isBuiltin
        True!

    method ownInterfaces
        value interfaces collect: { |each| { value: each } }!

    method visitBy: visitor
        visitor visitBuiltin: self!

    method __addInterface: anInterface
        value __addInterface: anInterface!

    method __addDirectMethod: aDirectMethod
        value __addDirectMethod: aDirectMethod!

    method __addInstanceMethod: anInstanceMethod
        value __addInstanceMethod: anInstanceMethod!
end

class AstDefine { global body frameSize compilerInfo }
    is AstDefinition

    direct method name: name
                  body: body
                  frameSize: frameSize
                  env: env
        let global = env global: name.
        let theDefine = self global: global
                             body: body
                             frameSize: frameSize
                             compilerInfo: CompilerInfo new.
        global define: theDefine.
        theDefine!

    method body: newBody
        body = newBody.
        self!

    method name
        global name!

    method isDynamic
        global isDynamic!

    method eval
        global hasValue
            ifTrue: { return global eval }.
        let result = AstInterpreter
                         evalDefine: body
                         frameSize: frameSize.
        global value: result!

    method toString
        "#<AstDefine {self name}>"!

    method visitBy: visitor
        visitor visitDefine: self!
end

class StandardDirectMethod { selector home }
    is AstMethod

    method isStandard
        True!

    method isDirect
        True!

    method methodHomeName
        Name mangleMetaclass: self home!

    method methodFunctionName
        Name mangleStandardMethod: selector!

    method displayOn: stream
        stream writeString: "#<StandardDirectMethod {home name} {selector name}>"!
end

define StandardDirectMethodSelectors
    [#includes:, #name, #classOf]!

class AstInterface { name _interfaces _directMethods _instanceMethods
                     _allInterfaces global _env compilerInfo }
    is AstDefinition
    is AstMethodHome

    direct method name: name
                  interfaces: interfaces
                  env: env
        let global = env global: name.
        let theInterface = self
                               name: name
                               _interfaces: interfaces
                               _directMethods: False
                               _instanceMethods: False
                               _allInterfaces: False
                               global: global
                               _env: env
                               compilerInfo: CompilerInfo new.
        global define: theInterface.
        theInterface!

    method eval
        global hasValue
            ifTrue: { return global eval }.
        { let result = Interface
                           new: name
                           interfaces: self interfaceObjects
                           directMethods: self ownDirectMethods
                           instanceMethods: self ownInstanceMethods.
          _env classes at: result put: self.
          global value: result }
        finally: { _env = False }!

    method isClass
        False!

    method _interfaces
        _interfaces!

    method _directMethods
        _directMethods!

    method _instanceMethods
        _instanceMethods!

    method _readerMethods
        []!

    method slots
        []!

    method _allInterfaces
        _allInterfaces!

    method _allInterfaces: list
        (_allInterfaces is False) assert: "Oops, _allInterfaces already set!".
        _allInterfaces = list asArray!

    method _directMethods: methods
        _directMethods = methods!

    method _instanceMethods: methods
        _instanceMethods = methods!

    method visitBy: visitor
        visitor visitInterfaceDefinition: self!

    method toString
        "#<AstInterface {name}>"!
end

class AstClass { name superclass _slots
                 _interfaces _directMethods _instanceMethods
                 _allInterfaces global _env compilerInfo }
    is AstDefinition
    is AstMethodHome

    direct method name: name
                  slots: slots
                  interfaces: interfaces
                  env: env
        self name: name
             slots: slots
             interfaces: interfaces
             env: env
             isBuiltin: False!

    direct method name: name
                  slots: slots
                  interfaces: interfaces
                  env: env
                  isBuiltin: isBuiltin
        let global = env global: name.
        let theClass = self
                           name: name
                           superclass: False
                           _slots: slots
                           _interfaces: interfaces asList
                           _directMethods: False
                           _instanceMethods: False
                           _allInterfaces: False
                           global: global
                           _env: env
                           compilerInfo: CompilerInfo new.
        isBuiltin
            ifTrue: { global redefine: theClass }
            ifFalse: { global define: theClass }.
        theClass!

    direct method name: name
                  superclass: superclass
                  interfaces: interfaces
                  env: env
        let global = env global: name.
        let theClass = self
                           name: name
                           superclass: superclass
                           _slots: False
                           _interfaces: ([superclass] append: interfaces)
                           _directMethods: False
                           _instanceMethods: False
                           _allInterfaces: False
                           global: global
                           _env: env
                           compilerInfo: List new.
        global define: theClass.
        theClass!

    method isClass
        True!

    method ifSuperclass: then ifNot: else
        superclass is False
            ifTrue: else
            ifFalse: then!

    method slots
        self
            ifSuperclass: { -- Debug println: "slots for {superclass definition}".
                            superclass definition slots }
            ifNot: { _slots }!

    method isBuiltin
        self
            ifSuperclass: { superclass definition isBuiltin }
            ifNot: { False }!

    method _interfaces
        _interfaces!

    method _directMethods
        _directMethods!

    method _instanceMethods
        _instanceMethods!

    method _allInterfaces
        _allInterfaces!

    method _allInterfaces: list
        (_allInterfaces is False) assert: "Oops, _allInterfaces already set!".
        _allInterfaces = list asArray!

    method _directMethods: methods
        _directMethods = methods!

    method _instanceMethods: methods
        _instanceMethods = methods!

    method markFunction
        self
            ifSuperclass: { superclass definition markFunction }
            ifNot: { "foo_mark_instance" }!

    method _readerMethods
        self _methodDictionary:
            ((self slots reject: #isPrivate)
                 collect: { |each|
                            AstReaderMethod home: self slot: each })!

    method eval
        global hasValue
            ifTrue: { return global eval }.
        { let slots = self slots asArray.
          let result
              = Class
                  new: name
                  slots: slots
                  interfaces: self interfaceObjects
                  directMethods: (self ownDirectMethods
                                      reject: #isConstructor)
                  instanceMethods: self ownInstanceMethods.
          -- KLUDGE: Bootstrap implementation doesn't do layouts,
          -- but the layout object is how we access slots in objects.
          -- Put in a dummy one that uses the bootstrap host __atSlot:
          -- methods.
          Foolang isSelfHosted
              ifFalse: { let layout = Layout dummy.
                         slots do: { |each| each layout: layout } }.
          _env classes put: self at: result.
          global value: result }
        finally: { _env = False }!

    method constructor
        self slots
            ifEmpty: { #new }
            ifNotEmpty: { Selector
                              intern: (StringOutput
                                        with: { |out|
                                                self slots
                                                    do: { |each|
                                                          out print: each name.
                                                          out print: ":" } }) }!

    method visitBy: visitor
        visitor visitClassDefinition: self!

    method toString
        "#<AstClass {name}>"!
end

-- FIXME: Not happy with layout being public. Should be
-- able to hand out a single slot without exposing the
-- whole instance through the layout. However, subclasses
-- have shared slots and layout, but class_target_ext.foo
-- gets slots and creates the layout -- so it needs the
-- access. Should probably pass in the layout there instead.
class AstSlot { name index type layout }
    is Object

    direct method name: name
                  index: index
                  type: type
        self name: name
             index: index
             type: type
             layout: False!

    method referenceUpwards: offset
        AstSlotRef slot: self!

    method isPrivate
        name isPrivate!

    method hasLayout
        return (layout is False) not!

    method layout: new
        layout is False
            assert: "AstSlot layout already set!".
        layout = new!

    method at: object
        layout is False
            ifTrue: { panic "{self} layout not set, cannot read from {object}" }.
        layout at: index
               in: object!

    method put: value at: object
        layout is False
            ifTrue: { panic "{self} layout not set, cannot write to {object}" }.
        layout put: value
               at: index
               in: object!

    method displayOn: stream
        stream writeString: "#<AstSlot ".
        stream print: name.
        stream writeString: ">"!
end

interface AstMethod
    is Object

    required method selector
    required method isDirect

    method isRequired
        False!

    method returnType
        TheUnknownType!

    method visitBy: visitor
        visitor visitMethod: self!

    method hasBody
        False!

    method isConstructor
        False!

    method name
        self selector name!

    method isBuiltin
        False!

    method isStandard
        False!

    method isDefined
        True!

    method methodFunctionName
        Name mangleMethod: self!

    method methodHomeName
        Name mangleMethodClass: self!

    method invoke: arguments on: receiver
        self selector
            sendTo: receiver
            with: arguments!
end

class AstConstructorMethod { home }
    is AstMethod

    method visitBy: visitor
        visitor visitMethod: self!

    method displayOn: stream
        stream writeString: "#<AstConstructorMethod {home name}>"!

    method isConstructor
        True!

    method isDirect
        True!

    method selector
        home constructor!

    method hasNonLocalReturns
        False!

    method returnType
        home!
end

class AstReaderMethod { home slot }
    is AstMethod

    method selector
        -- KLUDGE: host vs target difference
        slot name toSelector!

    method signature
        []!

    method arity
        0!

    method frameSize
        0!

    method isDirect
        False!

    method hasNonLocalReturns
        False!

    method stackContext
        True!

    method argumentVariables
        []!

    method returnType
        -- FIXME: Since setters check the type, the readers don't
        -- need to -- but it would be nice for senders to know the type.
        -- So returnType needs to be split into returnTypeCheck and
        -- declaredReturnType?
        Any!

    method body
        slot referenceUpwards: False!

    method printOn: stream
        stream print: "#<AstReaderMethod {home name}#{slot name}>"!
end

class AstDefinedMethod { home selector argumentVariables returnType _body frameSize
                         isDirect isRequired hasNonLocalReturns hasNonLocalReferences
                         compilerInfo }
    is AstMethod

    direct method home: home
                  selector: selector
                  argumentVariables: argumentVariables
                  returnType: returnType
                  isDirect: isDirect
                  isRequired: isRequired
        self
            home: home
            selector: selector
            argumentVariables: argumentVariables
            returnType: returnType
            _body: False
            frameSize: False
            isDirect: isDirect
            isRequired: isRequired
            hasNonLocalReturns: False
            hasNonLocalReferences: False
            compilerInfo: CompilerInfo new!

    method noteNonLocalReturn
        hasNonLocalReturns = True!

    method noteNonLocalReference
        hasNonLocalReferences = True!

    method stackContext
        hasNonLocalReferences is False!

    method hasBody
        self isRequired not!

    method body: bodyVal
        isRequired
            ifTrue: { Error raise: "Required methods have no body: {selector}" }.
        _body = bodyVal.
        self!

    method body
        isRequired
            ifTrue: { Error raise: "{self} is a required method: it has no body." }.
        _body is False
            ifTrue: { Error raise: "Body of {self} is not set yet." }.
        _body!

    method frameSize: frameSizeVal
        frameSize is False assert: "frame size not set".
        frameSize = frameSizeVal!

    method addTemp
        let offset = frameSize.
        frameSize = frameSize + 1.
        offset!

    method invoke: arguments on: receiver
        AstInterpreter
            invokeMethod: self
            on: receiver
            with: arguments!

    method signature
        argumentVariables collect: { |var| var type }!

    -- FIXME: duplicate methods below

    method arity
        argumentVariables size!

    method argumentCount
        argumentVariables size!

    method displayOn: stream
        self isDirect
            ifTrue: { stream print: "#<AstMethod {home name} classOf{selector}>" }
            ifFalse: { stream print: "#<AstMethod {home name}{selector}>" }!

    method toString
        StringOutput with: { |out| self printOn: out }!
end

interface AstNode
    is Ast

    method isImmediate
        False!

    method markTail
        False!

    method isTail
        False!

    required method visitBy: visitor
end

class AstArray { entries }
    is AstNode

    method entries: newEntries
        entries = newEntries.
        self!

    method type
        TheArrayType!

    method visitBy: visitor
        visitor visitArray: self!

    method parts
        entries!
end

class AstTypecheck { value assertType }
    is AstNode

    direct method value: value type: type
        type is Any
            => { return value }.
        (value type isSubtypeOf: type)
            => { return value }.
        self
            value: value
            assertType: type!

    method value: newValue
        value = newValue.
        self!

    method type
        (AstGlobal includes: assertType)
            ifTrue: { DefinitionType definition: assertType definition }
            ifFalse: { HostType type: assertType }!

    method visitBy: visitor
        visitor visitTypecheck: self!

    method parts
        [value, assertType]!
end

class AstRecord { name slots values }
    is AstNode

    method values: newValues
        values = newValues.
        self!

    method visitBy: visitor
        visitor visitRecord: self!

    method parts
        [name, slots, values]!
end

class AstDictionary { entries }
    is AstNode

    method entries: newEntries
        entries = newEntries.
        self!

    method visitBy: visitor
        visitor visitDictionary: self!

    method parts
        entries!
end

define _Unevaluated { name: "<unevaluated>" }!
define _BeingEvaluated { name: "<being-evaluated>" }!
define _Evaluated { name: "<evaluated>" }!

interface AstGlobalVariable
    is AstNode

    direct method name: name
        self
            name: name
            _id: False
            _value: False
            _state: _Unevaluated
            _definition: False
            sources: List new!

    direct method name: name definition: definition
        self
            name: name
            _id: False
            _value: False
            _state: _Unevaluated
            _definition: definition
            sources: List new!

    method compilerInfo
        self definition compilerInfo!

    method isBuiltin
        self isDefined
            ifTrue: { self _definition isBuiltin }
            ifFalse: { False }!

    method isImmediate
        True!

    method isUndefined
        self _definition is False!

    method isDefined
        self isUndefined not!

    method definition
        self assertDefined: True.
        self _definition!

    method define: definition
        self assertDefined: False.
        self _definition: definition.
        self isDefined assert: "invalid definition for global".
        definition!

    method assertDefined: wanted
        self isDefined is wanted
            ifFalse: { wanted
                           ifTrue: { Error raise: self noteUndefined }
                           ifFalse: { Error raise: "Global is already defined: {self name}" } }!

    method eval
        self _state is _Evaluated
            ifFalse: { self assertDefined: True.
                       self _state is _BeingEvaluated
                           ifTrue: { Error raise: "Cyclic definition: {self name}" }.
                       self _state: _BeingEvaluated.
                       self _definition eval.
                       self _state is _Evaluated
                           assert: "Evaluating definition did not set global" }.
        self _value!

    method value: value
        self hasValue not
            assert: "Cannot set value of global: already has value".
        self isDefined
            assert: "Cannot set value of global: undefined".
        self _value: value.
        self _state: _Evaluated.
        value!

    method hasValue
        self _state is _Evaluated!

    method isNonLocal
        -- FIXME: This refers to capturing reference. Terrible
        -- name here because I don't have AstGlobalRefs separately.
        False!

    method isConstant
        True!

    method referenceUpwards: _offset
        self!

    method toString
        "#<{self classOf name} {self name} {self _state name} ({self _id})>"!

    method noteUndefined
        self sources
            ifEmpty: { return "Undefined variable: {self name} (no source)" }.
        let note = "Undefined variable: {self name}".
        "{note}\n{self sources first note: note}"!

    method warnIfUndefined
        self isUndefined
            ifTrue: { Output debug println: "\nWARNING: {self noteUndefined}" }!

    method withSource: sourceLocation
        self sources add: sourceLocation.
        self!
end

class AstGlobal { name::String
                  _id
                  _value
                  _state
                  _definition
                  sources }
    is AstGlobalVariable

    method isDynamic
        False!

    method _id
        _id!

    method type
        EqType value: self eval!

    method includes: value
        self eval includes: value!

    method idOr: block
        -- A bit of a KLUDGE: builtin globals have definitions
        -- in main.c, and we want their names to be consistent.
        --
        -- See also: CompilerBuiltin#idOr:
        self isBuiltin
            ifTrue: { _id = "" }.
        _id is False
            ifTrue: { _id = block value }
            ifFalse: { _id }!

    method _value: new
        _value = new!

    method _value
        _value!

    method _state: new
        _state = new!

    method _state
        _state!

    method _definition: new
        _definition = new!

    method _definition
        _definition!

    method visitBy: visitor
        visitor visitGlobal: self!

    method ownInterfaces
        self definition ownInterfaces!

    method assign: value
        panic "Cannot assign to global! {name} = {value}"!

    -- KLUDGE: Currently types are either Any or AstGlobals. This is marginally
    -- better than the previous mess, but still not nice.
    method typecheck: object
        self eval typecheck: object!

    method redefine: newDefinition
        _definition is newDefinition
            => { return newDefinition }.
        self hasValue not
            assert: "Cannot redefine a materialized global: {name}".
        -- FIXME: This is a bit dodgy: the usage is currently for replacing
        -- host definitions of builtins in transpiler, but it seems to me that
        -- value should be updated too if it has been materialized...
        --
        -- (See #redefineUsing:)
        _definition = newDefinition.
        newDefinition global: self.
        newDefinition!

    method redefineUsing: global
        _definition = global definition.
        sources = global sources.
        global hasValue
            ifTrue: { self value: global eval }!

end

class AstDynamic { name::String
                   _id
                   _value
                   _state
                   _definition
                   sources }
    is AstGlobalVariable

    -- FIXME: AstDefine should include type information.
    method type
        TheUnknownType!

    method isDynamic
        True!

    method _id
        _id!

    method idOr: block
        _id is False
            ifTrue: { _id = block value }
            ifFalse: { _id }!

    method _definition
        _definition!

    method _definition: new
        _definition = new!

    method _value
        _value!

    method _value: new
        _value = new!

    method _state
        _state!

    method _state: new
        _state = new!

    method bind: newValue in: block
        let oldValue = self eval.
        _value = newValue.
        { block value }
            finally: { _value = oldValue }!

    method visitBy: visitor
        visitor visitDynamic: self!

    method redefine: newDefinition
        self hasValue not
            assert: "Cannot redefine a materialized global".
        -- FIXME: This is a bit dodgy: the usage is currently for replacing
        -- host definitions of builtins in transpiler, but it seems to me that
        -- value should be updated too if it has been materialized...
        --
        -- (See #redefineUsing:)
        _definition = newDefinition.
        newDefinition global: self.
        newDefinition!
end

class AstConstantRef { value source }
    is AstNode

    method type
        EqType value: value!

    method isImmediate
        True!

    method visitBy: visitor
        visitor visitConstant: self!

    method isConstant
        True!

    method toString
        "<AstConstantRef {value}>"!
end

class AstIs { left right }
    is AstNode

    method type
        TheBooleanType!

    method left: newLeft right: newRight
        left = newLeft.
        right = newRight.
        self!

    method visitBy: visitor
        visitor visitIs: self!
end

class AstSeq { first then }
    is AstNode

    method first: newFirst then: newThen
        first = newFirst.
        then = newThen.
        self!

    method visitBy: visitor
        visitor visitSeq: self!
end

class AstReturn { from value isLocal isTail }
    is AstNode

    direct method from: from value: value isLocal: local
        self from: from
             value: value
             isLocal: local
             isTail: False!

    method markTail
        -- Do NOT mark value recursively, there may be a
        -- pending typecheck!
        isTail = True!

    method value: newValue
        value = newValue.
        self!

    method visitBy: visitor
        visitor visitReturn: self!
end

class AstPanic { value }
    is AstNode

    method value: newValue
        value = newValue.
        self!

    method visitBy: visitor
        visitor visitPanic: self!
end

class AstSend {
        receiver
        selector
        arguments
        source
    }
    is AstNode

    method type
        TheUnknownType!

    method receiver: newReceiver arguments: newArguments
        receiver = newReceiver.
        arguments = newArguments.
        self!

    method visitBy: visitor
        visitor visitSend: self!

    method printOn: stream
        stream print: "#<AstSend {selector} to {receiver} with {arguments}>"!
end

class AstCallMethod { target receiver arguments source }
    is AstNode

    method target: newTarget receiver: newReceiver arguments: newArguments
        target = newTarget.
        receiver = newReceiver.
        arguments = newArguments.
        self!

    method selector
        target selector!

    method type
        HostType type: target returnType!

    method visitBy: visitor
        visitor visitCallMethod: self!

    method printOn: stream
        stream print: "#<AstCallMethod {target home name}#{target selector name}>"!
end

class AstBlock { body argumentVariables returnType frameSize }
    is AstNode

    method body: newBody
        body = newBody.
        self!

    method type
        ClosureType
            argumentTypes: (argumentVariables collect: #type)
            returnType: returnType!

    method selector
        self argumentCount is 0
            ifTrue: { return #value }.
        self argumentCount is 1
            ifTrue: { return #value: }.
        self argumentCount is 2
            ifTrue: { return #value:value: }.
        self argumentCount is 3
            ifTrue: { return #value:value:value: }.
        #apply:!

    method visitBy: visitor
        visitor visitBlock: self!

    method addTemp
        let offset = frameSize.
        frameSize = frameSize + 1.
        offset!

    method argumentCount
        argumentVariables size!
end

class AstSelfInstanceRef { type }
    is AstNode

    method isImmediate
        True!

    method visitBy: visitor
        visitor visitSelfInstance: self!
end

class AstSelfClassRef { type }
    is AstNode

    method isImmediate
        True!

    method visitBy: visitor
        visitor visitSelfClass: self!
end

class AstSlotRef { slot source }
    is AstNode

    method type
        let type = slot type.
        (AstGlobal includes: type)
            ifTrue: { DefinitionType definition: type definition }
            ifFalse: { HostType type: type }!

    method isImmediate
        True!

    method isNonLocal
        False!

    direct method slot: slot
        self
            slot: slot
            source: False!

    method visitBy: visitor
        visitor visitSlotRef: self!

    method assign: value
        AstSlotSet
            slot: slot
            value: value!

    method withSource: sourceLocation
        (source is False) assert.
        source = sourceLocation.
        self!
end

class AstSlotSet { slot value }
    is AstNode

    method value: newValue
        value = newValue.
        self!

    method visitBy: visitor
        visitor visitSlotSet: self!
end

class AstBindLexical { variable value _body }
    is AstNode

    direct method variable: variable value: value body: body
        let binding = self variable: variable
                           value: value
                           _body: body.
        variable boundBy: binding.
        binding!

    method body
        _body!

    method value: newValue body: newBody
        value = newValue.
        _body = newBody.
        self!

    method visitBy: visitor
        visitor visitBindLexical: self!
end

class AstBindDynamic { variable value body }
    is AstNode

    method value: newValue body: newBody
        value = newValue.
        body = newBody.
        self!

    method visitBy: visitor
        visitor visitBindDynamic: self!
end

class AstLexicalRef { variable frameOffset::Integer source }
    is AstNode

    method type
        let type = variable type.
        (Type includes: type)
            => { return type }.
        (AstGlobal includes: type)
            => { return DefinitionType definition: type definition }.
        HostType type: type!

    method isImmediate
        True!

    method isNonLocal
        frameOffset > 0!

    direct method variable: variable frameOffset: offset
        self
            variable: variable
            frameOffset: offset
            source: False!

    method withSource: newSource
        (source is False) assert.
        source = newSource.
        self!

    method visitBy: visitor
        visitor visitLexicalRef: self!

    method assign: value
        variable noteAssignment: source.
        AstLexicalSet
            variable: variable
            frameOffset: frameOffset
            value: value!

    method displayOn: stream
        stream writeString: "#<AstLexicalRef ".
        stream writeString: variable name.
        stream writeString: ">"!
end

class AstCascadeReceiver {}
    is AstNode

    method type
        TheCascadeReceiverType!

    method isImmediate
        True!

    method visitBy: visitor
        visitor visitCascadeReceiver: self!
end

define TheAstCascadeReceiver AstCascadeReceiver new!

class AstCascade { receiver messages }
    is AstNode

    method receiver: newReceiver messages: newMessages
        receiver = newReceiver.
        messages = newMessages.
        self!

    method visitBy: visitor
        visitor visitCascade: self!
end

class AstLexicalSet { variable frameOffset::Integer value }
    is AstNode

    method value: newValue
        value = newValue.
        self!

    method visitBy: visitor
        visitor visitLexicalSet: self!
end
