class FileModuleDictionary { _dict }
    direct method new: dict
        self _dict: dict!
    method at: fullPath ifNone: block
        -- Debug println: "FILE: {fullPath}".
        let path = fullPath rest butlast.
        let root = fullPath first.
        let filepath = _dict at: root
                             ifNone: block.
        { path isEmpty }
            whileFalse: { filepath = filepath / path pop }.
        filepath / "{fullPath last}.foo"
            ifExists: { |foopath|
                        foopath file forRead open: { |f| f readString } }
            ifDoesNotExist: block!
end

extend Dictionary
    method asFileModules
        FileModuleDictionary new: self!
end

class Debug {}
    direct method perform: name with: arguments
        name sendTo: Output debug with: arguments!
    direct method pad: n println: message
        n times: { self print: " " }.
        self println: message!
end

class DebugTracer { name }
    direct method trace: thing
        Debug println: thing!
    direct method perform: selectorName with: arguments
        (Selector intern: selectorName) parts
            doWithIndex: { |each index|
                           index > 1
                               ifTrue: { Debug print: " #" }
                               ifFalse: { Debug print: "#" }.
                           Debug print: each.
                           Debug print: " ".
                           Debug print: (arguments at: index) }.
        Debug newline!
    method trace: thing
        Debug print: name.
        Debug println: thing!
    method perform: selectorName with: arguments
        Debug print: name.
        (Selector intern: selectorName) parts
            doWithIndex: { |each index|
                           index > 1
                               ifTrue: { Debug print: " #" }
                               ifFalse: { Debug print: "#" }.
                           Debug print: each.
                           Debug print: " ".
                           Debug print: (arguments at: index) }.
        Debug newline!
end

class NullTracer {}
    direct method perform: selector with: arguments
        False!
end
