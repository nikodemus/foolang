import .astRewriter.AstRewriter
import .ast.*
import .utils.*

interface AstOptimizer
    direct method optimize: ast in: env
        {
            let resolver = KnownMethodResolver new: env.
            ast = ast visitBy: resolver.
            resolver didNothing
                => { return ast }.

            -- Trivial methods are such that inlining them
            -- never blows up code size, so we can do this
            -- unconditionally.
            let inliner = TrivialMethodInliner new.
            ast = ast visitBy: inliner.
            inliner didNothing
                => { return ast }

        } loop!
end

class KnownMethodResolver { env didNothing }
    is AstRewriter

    direct method new: env
        self env: env
             didNothing: True!

    method visitSend: aSend
        let recv = aSend receiver visitBy: self.
        let args = aSend arguments collect: { |each| each visitBy: self }.
        let type = recv type.
        (type findMethod: aSend selector in: env)
            => { |aMethod|
                 didNothing = False.
                 Debug println: "known: {aMethod}".
                 return AstCallMethod
                     target: aMethod
                     receiver: recv
                     arguments: args
                     source: aSend source }.
        aSend
            receiver: recv
            arguments: args!
end

class TrivialMethodInliner { didNothing }
    is AstRewriter

    direct method new
        self didNothing: True!

    method visitCallMethod: aCall
        Debug println: "tryInline: {aCall}".
        aCall target isTrivial
            ifTrue: { didNothing = False.
                      self inline: aCall }
            ifFalse: { aCall }!

    method inline: aCall
        Debug println: "inline: {aCall}".
        aCall body!
end

extend Ast
    method isTrivial
        False!
end

extend AstMethod
    method isTrivial
        self body isTrivial
            ifTrue: { True }
            ifFalse: { Debug println: "nontrivial: {self} == {self body}".
                       False }!
end

extend AstConstantRef
    method isTrivial
        True!
end
