import .ast.*
import .syntaxVisitor.SyntaxVisitor

class SyntaxTranslator { env }
    is SyntaxVisitor

    direct method translate: syntax in: env
        syntax
            visitBy: (SyntaxTranslator env: env)!

    method addVariable: name
        SyntaxTranslator env: (env addVariable: name)!

    method visitLiteral: aLiteral
        AstConstantRef value: aLiteral value!

    method visitHexLiteral: aLiteral
        self visitLiteral: aLiteral!

    method visitBinaryLiteral: aLiteral
        self visitLiteral: aLiteral!

    method visitValueTypeDeclaration: aNode
        AstTypecheck
            value: (aNode value visitBy: self)
            type: (aNode type visitBy: self)!

    method visitArray: anArray
        AstArray entries: (anArray entries
                               collect: { |each| each visitBy: self })!

    method visitRecord: aRecord
        let recordName = StringOutput
                             with: { |out|
                                     aRecord entries
                                         do: { |each| out print: each key } }.
        AstRecord
            name: (Selector new: recordName)
            entries: (aRecord entries
                          collect: { |each| each value visitBy: self })!

    method visitDictionary: aDictionary
        AstDictionary
            entries: (aDictionary entries
                          collect: { |each| { key: (each key visitBy: self),
                                              value: (each value visitBy: self) }})!

    method visitSeq: aSeq
        -- FIXME: Would be nicer to flatten this out.
        AstSeq
            first: (aSeq first visitBy: self)
            then: (aSeq then visitBy: self)!

    method visitReturn: aReturn
        AstReturn value: (aReturn value visitBy: self)!

    method visitPrefixComment: aComment
        AstComment
            comment: aComment comment
            value: (aComment value visitBy: self)
            source: aComment source!

    method visitSuffixComment: aComment
        AstComment
            comment: aComment comment
            value: (aComment value visitBy: self)
            source: aComment source!

    method visitLet: aLet
        let bodyVisitor = self addVariable: aLet name.
        let var = bodyVisitor env bindings first.
        (var name == aLet name) assert.
        AstBindLexical
            variable: var
            value: (aLet value visitBy: self)
            body: (aLet body visitBy: bodyVisitor)!

    method visitPrefixMessage: aMessage
        AstSend
            receiver: (aMessage receiver visitBy: self)
            selector: (Selector name: "prefix{aMessage selector name}")
            arguments: []
            source: aMessage source!

    method visitUnaryMessage: aMessage
        AstSend
            receiver: (aMessage receiver visitBy: self)
            selector: aMessage selector
            arguments: []
            source: aMessage source!

    method visitBinaryMessage: aMessage
        AstSend
            receiver: (aMessage receiver visitBy: self)
            selector: aMessage selector
            arguments: [aMessage argument visitBy: self]
            source: aMessage source!

    method visitKeywordMessage: aMessage
        AstSend
            receiver: (aMessage receiver visitBy: self)
            selector: aMessage selector
            arguments: (aMessage arguments collect: { |arg| arg visitBy: self })
            source: aMessage source!

    method visitIs: anIs
        AstIs
            left: (anIs left visitBy: self)
            right: (anIs right visitBy: self)!

    method visitSelf: aSelf
        AstSelfRef new!

    method visitVariable: aVariable
        env reference: aVariable name!

    method visitInterfaceRef: aRef
        env global: aRef name!

    method visitAssign: anAssign
        (env reference: anAssign variable name)
            assign: (anAssign value visitBy: self)!

    method visitParens: aParens
        aParens body visitBy: self!

    method visitBlock: aBlock
        let bodyEnv = env newFrame addVariables: aBlock parameters.
        AstBlock
            body: (aBlock body visitBy: (SyntaxTranslator env: bodyEnv))
            argumentCount: aBlock parameters size
            frameSize: bodyEnv frame size!

    method visitDefine: aDefine
        let bodyVisitor = SyntaxTranslator env: env newFrame.
        AstDefine
            name: aDefine name
            body: (aDefine body visitBy: bodyVisitor)
            frameSize: bodyVisitor env frame size!

    method visitMethod: aMethod
        let signature = aMethod signature.
        let bodyEnv = env newFrame addVariables: signature parameters.
        AstMethod
            selector: signature selector
            argumentCount: signature parameters size
            body: (aMethod body visitBy: (SyntaxTranslator env: bodyEnv))
            frameSize: bodyEnv frame size!

    method visitClass: aClass
        let instanceMethodVisitor = SyntaxTranslator env: (env addSlots: aClass slots).
         -- FIXME: should fetch interface object instead of using the name
        AstClass
            name: aClass name
            slots: aClass slots
            interfaces: (aClass interfaces
                             collect: { |i| i visitBy: self })
            directMethods: (aClass directMethods
                                collect: { |m| m visitBy: self })
            instanceMethods: (aClass instanceMethods
                          collect: { |m| m visitBy: instanceMethodVisitor })!

    method visitInterface: anInterface
         -- FIXME: should fetch interface object instead of using the name
        AstInterface
            name: anInterface name
            interfaces: (anInterface interfaces
                             collect: { |i| i visitBy: self })
            directMethods: (anInterface directMethods
                                collect: { |m| m visitBy: self })
            instanceMethods: (anInterface instanceMethods
                                  collect: { |m| m visitBy: self })!
end
